<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Depth Scan</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<div class="main">
	<div id="main">
		<div id="content">
			<div id="title">Depth Scan</div>
			<div id="header">	  	
				<div id="baseline">
					<p id="date">2017/07/01</p>
					<h1>#GLSL Depth scan</h1>
					<p id="description">Depthmap based segmentation. UI design inspired by Blade Runner UI</p>
				</div>		  	
				<div id="author">â€” Code & Design by <a href="https://www.arivaux.com" target = "_blank">Alexandre Rivaux</a></div>
			</div>
		</div>
		<div id="container"></div>	
	</div>
</div>
<!-- GLSL SCRIPT -->
<!-- vertex shader -->
<script id="vertexShader" type="x-shader/x-vertex">
	void main() {
		gl_Position = vec4(position, 1.0);
	}
</script>

<!-- fragment shader -->
<script id="fragmentShader" type="x-shader/x-fragment">
		#define PI 3.14159265359
		#define TWO_PI 6.28318530718
		
		uniform vec2 resolution;
		uniform float time;
		uniform float target;
		uniform sampler2D texture;
		uniform sampler2D UIText;
	
		float random(float value){
			return fract(sin(value) * 43758.5453123);
		}

		float random(vec2 tex){
			return fract(sin(dot(tex.xy, vec2(12.9898, 78.233))) * 43758.5453123);
		}

		float random(vec3 tex){
			//return fract(sin(x) * offset);
			return fract(sin(dot(tex.xyz, vec3(12.9898, 78.233, 12.9898))) * 43758.5453123);//43758.5453123);
		}


		vec4 addGrain(vec2 uv, float time, float grainIntensity){
			float grain = random(fract(uv * time)) * grainIntensity;
			return vec4(vec3(grain), 1.0);
		}

		float addStreamLine(vec2 uv, float rows, float height, float smooth_){
			vec2 uvstream = uv * vec2(1.0, rows);
			float distFromCenter = abs(0.5 - fract(uvstream.y));
			float edge = smoothstep(height - smooth_*0.5, height + smooth_*0.5, distFromCenter);
			/*
			vec2 iuv = floor(uvstream);
			float modrow = mod(iuv.y, 2.0);
			*/
			return edge;
		}

		vec2 lensDistort(vec2 uv, float power, float radInc)
		{
			float theta  = atan(uv.y, uv.x);
			float radius = length(uv);
			radius = pow(radius, power) * radInc;
			uv.x = radius * cos(theta);
			uv.y = radius * sin(theta);
			return 0.5 * (uv + 1.0);
		}

		float rect(vec2 uv, vec2 length, float smooth_){
			float dx = abs(uv.x - 0.5);
			float dy = abs(uv.y - 0.5);
			float lenx = 1.0 - smoothstep(length.x - smooth_, length.x + smooth_, dx);
			float leny = 1.0 - smoothstep(length.y - smooth_, length.y + smooth_, dy);

			return lenx * leny;
		}
		
		void main(){
			vec2 uv = gl_FragCoord.xy / resolution.xy;
			uv = lensDistort(uv.xy * 2.0 - 1.0, 1.1, 1.1);
		
			vec2 offset = vec2(random(time) * 0.0035, 0.0);
			vec2 nuv = uv + vec2(0.0, fract(time * 0.1));
			vec2 nuvThick = uv + vec2(0.0, fract(time * 0.15));

			float numberOfline = 100.0;
			vec2 iuv = floor(uv * numberOfline) / numberOfline;

			float amplitude = 0.00025 + random(time) * 0.001;
			float period = 20.0;
			float wave = sin(TWO_PI * period * (iuv.y + time + random(iuv.y) * 0.5 + 0.5)) * amplitude;
			uv.x += wave;

			float amplitudeBig = 0.005;
			float periodBig = 0.75;
			float waveBig = sin(TWO_PI * periodBig * uv.y + time) * amplitudeBig;
			uv.x += waveBig;

			float distanceTex = texture2D(texture, uv).r;
			
		  	vec3 DepthSlice = vec3(0.0);
			#define iteration 2
			float smoothing = 0.1 / float(iteration);
			float thickness = 0.25 / float(iteration);
			for(int i=0; i<iteration; i++){
				float fi = float(i) / float(iteration);
				float targeti = fract(fi + target) * pow(distanceTex, 0.05);
				//targeti = clamp(targeti, 0.0, 0.979);

				float df1 = smoothstep((targeti - thickness * 0.5) - smoothing, (targeti - thickness * 0.5) + smoothing, 1.0 - distanceTex);
				float df2 = smoothstep((targeti + thickness * 0.5) - smoothing, (targeti + thickness * 0.5) + smoothing, 1.0 - distanceTex);
				float df = df1 - df2;

				DepthSlice += vec3(df);
			}

			vec4 grain = addGrain(uv, time, 0.1);

			float streamLines = addStreamLine(nuv, 2.0, 0.2, 0.25);
			streamLines = clamp(streamLines, 0.85, 1.0);

			float streamLinesThick = addStreamLine(nuvThick, 80.0, 0.15, 0.01);
			streamLinesThick = clamp(streamLinesThick, 0.85, 1.0);

			vec4 stream = vec4(streamLines, streamLines, streamLines, 1.0);
			vec3 RGB = mix(vec3(1.0, 0.0, 0.0), vec3(0.0, 1.0, 1.0), DepthSlice.r * pow(distanceTex, 1.15)) * pow(DepthSlice.r, 1.5);
			vec4 RGB4 = vec4(RGB + vec3(0.05, 0.0, 0.25), 1.0) * 1.25;

			float cols = 60.0;
			float ratio = resolution.x / resolution.y;
			vec2 colsrows = vec2(cols, cols / ratio);
			nuv = uv * colsrows;
			vec2 fuv = fract(nuv);
			iuv = floor(nuv);
			vec2 modiuv = step(mod(iuv, 4.0), vec2(0));
			float randDistribution = (modiuv.x * modiuv.y) * step(random(vec3(iuv, floor(mod(time, 1000.0)))), 0.1);

			float rectH = rect(fuv, vec2(1.0, 0.035), 0.0);
			vec4 rectH4 = vec4(vec3(rectH), 1.0);
			float rectV = rect(fuv, vec2(0.035, 1.0), 0.0);
			vec4 rectV4 = vec4(vec3(rectV), 1.0);

			vec4 frect = ((rectH4 + rectV4) * randDistribution) * 0.35;

			vec4 UItexture = texture2D(UIText, uv) * 0.35;
			
			gl_FragColor = (UItexture.a * vec4(1.0) + RGB4 + grain + frect) * streamLines * streamLinesThick;
		}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/87/three.min.js'></script>
<script src='https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js'></script>
<script src='https://www.arivaux.com/prototype/codevember2017/DepthUI/texData.js'></script><script  src="./script.js"></script>

</body>
</html>
