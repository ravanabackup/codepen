<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - A Pen by Liam Egan</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<script type="text/fragment" id="fragShader">#version 300 es
  precision highp float;

  uniform vec2 u_resolution;
  uniform float u_time;
  uniform vec2 u_mouse;
  uniform sampler2D s_noise;
  
  uniform float t_offset;
  
  float t;

  in vec2 v_uv;

  out vec4 colour;
  
  const vec3 ca = vec3(.8, .5, .4);
  const vec3 cb = vec3(.2, .4, .2); 
  const vec3 cc = vec3(2, 1, 1);
  const vec3 cd = vec3(0.,.25,.25);
  
  /* Structures */
  /* ---------- */
  struct Camera {
    vec3 ro;
    vec3 rd;
    vec3 forward;
    vec3 right;
    vec3 up;
    float FOV;
  };
  struct Light {
    vec3 pos;
    vec3 col;
    float str;
    float fo;
  };
  struct Cloud {
    float ld;
    float density;
    float weight;
    float dist;
    float travelled;
    float weightedTravel;
    vec3 sp;
    vec3 col;
    vec3 sn;
  };
  
  /* Utility */
  /* ---------- */
  Light getLight(in vec3 pos, in vec3 col, in float str, in float fo) {
    return Light(
      pos, col, str, fo
    );
  }
  Light getLight(in vec3 pos) {
    return getLight(
      pos, vec3(1), 1., 1.
    );
  }
  Cloud getCloud() {
    return Cloud(0.,0.,0.,1.,0., 0., vec3(0), vec3(0), vec3(0));
  }
  Camera getCamera(in vec2 uv, in vec3 pos, in vec3 target) {
    vec3 forward = normalize(target - pos);
    vec3 right = normalize(vec3(forward.z, 0., -forward.x));
    vec3 up = normalize(cross(forward, right));

    float FOV = 1.5;

    return Camera(
        pos,
        normalize(forward + FOV * uv.x * right + FOV * uv.y * up),
        forward,
        right,
        up,
        FOV
      );
  }
  vec3 pal( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
  {
    return a + b*cos( 6.28318*(c*t+d) );
  }
  vec2 getScreenSpace() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);

    return uv;
  }
  
  float pn( in vec3 p ) {
    vec3 i = floor(p); p -= i; p *= p*(3. - 2.*p);
    p.xy = texture(s_noise, (p.xy + i.xy + vec2(37, 17)*i.z + .5)/256., -100.).yx;
    return mix(p.x, p.y, p.z);
  }
  
  // Thanks to Shane for this one.
  // Basic low quality noise consisting of three layers of rotated, mutated 
  // trigonometric functions. Needs work, but sufficient for this example.
  float trigNoise3D(in vec3 p){


      float res = 0., sum = 0.;

      // IQ's cheap, texture-lookup noise function. Very efficient, but still 
      // a little too processor intensive for multiple layer usage in a largish 
      // "for loop" setup. Therefore, just one layer is being used here.
      float n = pn(p*8. + t*.5);


      // Two sinusoidal layers. I'm pretty sure you could get rid of one of 
      // the swizzles (I have a feeling the GPU doesn't like them as much), 
      // which I'll try to do later.

      vec3 t = sin(p.yzx*3.14159265 + cos(p.zxy*3.14159265+1.57/2.))*0.5 + 0.5;
      p = p*1.5 + (t - 1.5); //  + u_time*0.1
      res += (dot(t, vec3(0.333)));

      t = sin(p.yzx*3.14159265 + cos(p.zxy*3.14159265+1.57/2.))*0.5 + 0.5;
      res += (dot(t, vec3(0.333)))*0.7071;    

    return ((res/1.7071))*0.85 + n*0.15;
  }
  
float hash(float h) {
	return fract(sin(h) * 43758.5453123);
}
  vec3 hash33(vec3 p3)
{
	p3 = fract(p3 * vec3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yxz+33.33);
    return fract((p3.xxy + p3.yxx)*p3.zyx);

}

float noise(vec3 x) {
	vec3 p = floor(x);
	vec3 f = fract(x);
	f = f * f * (3.0 - 2.0 * f);

	float n = p.x + p.y * 157.0 + 113.0 * p.z;
	return mix(
			mix(mix(hash(n + 0.0), hash(n + 1.0), f.x),
					mix(hash(n + 157.0), hash(n + 158.0), f.x), f.y),
			mix(mix(hash(n + 113.0), hash(n + 114.0), f.x),
					mix(hash(n + 270.0), hash(n + 271.0), f.x), f.y), f.z);
}

float smin(float a, float b, float k) {
    float x = exp(-k * a);
    float y = exp(-k * b);
    return (a * x + b * y) / (x + y);
}

float sdBox( vec3 p, vec3 b )
{
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}
void rot(inout vec2 p, float a) {
  float s = sin(a);
  float c = cos(a);
  p *= mat2(c, -s, s, c);
}
  float sdBoxFrame( vec3 p, vec3 b, float e )
{
       p = abs(p  )-b;
  vec3 q = abs(p+e)-e;
  return min(min(
      length(max(vec3(p.x,q.y,q.z),0.0))+min(max(p.x,max(q.y,q.z)),0.0),
      length(max(vec3(q.x,p.y,q.z),0.0))+min(max(q.x,max(p.y,q.z)),0.0)),
      length(max(vec3(q.x,q.y,p.z),0.0))+min(max(q.x,max(q.y,p.z)),0.0));
}
  
  float world(vec3 p, inout float n, in Camera cam) {
    
    float raylength = length(cam.ro - p);
    
    float u_time = t*.5;
    
    vec3 _p = p+vec3(0,0,-10);
    
    _p.z = mod(_p.z, 10.) - 5.;
    
    _p.x = abs(_p.x) - sin(u_time * 5.);
    _p.y = abs(_p.y) - cos(u_time * 10.) * 2.;
    
    _p.x = abs(_p.x) - sin(u_time * 10.) * .5;
    _p.y = abs(_p.y) - cos(u_time * 5.) * 5.;
    
    _p.z = abs(_p.z) - cos(u_time * 5.);
    
    _p.xy = mod(_p.xy - 2.5, 5.) - 2.5;
    
    
    rot(_p.xz, u_time*5.);
    rot(_p.xy, u_time*2.5);
    rot(_p.zy, u_time*10.);
    
    return sdBoxFrame(_p, vec3(2.5), .5) * 10. + 20. * smoothstep(5., 4.5, raylength);
    // return sdBox(_p, vec3(2.5)) * 10. - .1 + 20. * smoothstep(10., 4.5, raylength);
    return mod((sdBox(_p, vec3(1.5)) - .1), 10.) + .5;
  }
  
  const int steps = 64;
  const float maxTravelled = 64.;
  const float maxDensity = 1.;
  const float distanceThreshold = .03;
  
  void march(inout Cloud c, in Camera cam, in Light l) {
    
    c.sn = -cam.rd;
    
    for (int i=0; i<steps; i++) {

      if((c.density>maxDensity) || c.travelled>maxTravelled)break;
      
      float n;

      vec3 camRay = cam.rd*c.travelled;
      c.sp = cam.ro + camRay; // Ray position.
      c.dist = world(c.sp, n, cam); // Closest distance to the surface... particle.
      
      float dist = max(.01, c.dist);
      
      if(c.dist < .2) c.dist = .35;
      
      dist = c.dist;
      
      float dweight = smoothstep(-1.6, 30.5, c.travelled);

      c.ld = (distanceThreshold - c.dist)*step(dist, distanceThreshold)*.005;
      c.weight = max(0., (1. - c.density)*c.ld);

      c.density += c.weight*(1.-distanceThreshold)*1./dist;

      vec3 ld = l.pos-c.sp; // Direction vector from the surface to the light position.
      float lDist = max(length(ld), .001); // Distance from the surface to the light.
      ld/=lDist; // Normalizing the directional light vector.

      // Using the light distance to perform some falloff.
      float atten = 1./(1. + lDist*.125 + lDist*lDist*.025);

      float diff = max(dot( c.sn, ld ), 0.);
      float spec = pow(max( dot( reflect(-ld, c.sn), -cam.rd ), 0. ), 4.);

      c.col += c.weight * 
        pal(c.sp.z*.1, 
            ca, cb, cc, cd
           ) *
        (dist*5. - .1)*(.1 + diff * .5 + spec*.1)*atten;
      // Much quicker version of the avove
      // c.col += c.weight*atten*1.25;

      float dt = max(dist*.2, .01) * max(.1, hash33(c.sp*255.).x);
      c.travelled += dt;
      c.weightedTravel += dt * dweight;

    }
  }

  void main() {
    vec2 uv = getScreenSpace();
    
    t = -1000. + u_time * 10.;
    // t = 0.;
    
    // vec2 tt = vec2( cos(t*.2), sin(t*.2) );
    
    Camera cam = getCamera(
      uv, 
      vec3(0, 0,0. + t), 
      vec3(0., 0., 1. + t)
    );
    Light light = getLight(vec3( -3, -7, 3.5 + t));
    Cloud cloud = getCloud();
    
    march(cloud, cam, light);
    
    vec3 col = cloud.col;

    col = mix(col, vec3(2.), cloud.weightedTravel*.008 / (maxTravelled/128.) * 2.);

    colour = vec4(sqrt(col),1);
  }
</script>
<!-- partial -->
  <script type="module" src="./script.js"></script>

</body>
</html>
