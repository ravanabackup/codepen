<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - GLSL: Refracted rays</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="webgl" width="500" height="1758"></canvas>

<script id="vertexShader" type="x-shader/x-vertex">
  attribute vec4 a_position;
  
  uniform mat4 u_modelViewMatrix;
  uniform mat4 u_projectionMatrix;
  
  void main() {
    gl_Position = a_position;
  }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
 precision highp float;
  precision highp int;
  
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  
  // movement variables
  vec3 movement = vec3(.0);
  
  const int maxIterations = 128;
  const float stopThreshold = 0.01;
  const float stepScale = .9;
  const float eps = 0.005;
  const vec3 clipColour = vec3(0.);
  const vec3 fogColour = vec3(0.);
  
  const vec3 light1_position = vec3(0, 1., -1.);
  const vec3 light1_colour = vec3(.8, .8, .85);
  
  struct Surface {
    int object_id;
    float distance;
    vec3 position;
    vec3 colour;
    float ambient;
    float spec;
  };
  
  vec3 path(float z) {
    return vec3(sin(z*.2)*4.,0.,z*5.);
    return vec3(0,0.,z*5.);
  }
  
  // This function describes the world in distances from any given 3 dimensional point in space
  float world(in vec3 position, inout int object_id, inout vec3 norm) {
    // float z = position.z * .17;
    // float c = cos(z);
    // float s = sin(z);
    // position.xy *= mat2(c, -s, s, c);
    
    vec3 pos = floor(position * .5);
    object_id = int(floor(pos.x + pos.y + pos.z)*2.);
    position = mod(position, 1.) - .5;
    float world = length(position) - .3;
    norm = normalize(position);
    position.xy -= path(position.z+100.).xy;
    return world;
  }
  float world(in vec3 position) {
    int dummy = 0;
    vec3 norm = vec3(0.);
    return world(position, dummy, norm);
  }
  
  vec3 getObjectColour(int object_id) {
    float modid = mod(float(object_id), 5.);
    if(modid == 0.) {
      return vec3(.0, 0., 0.5);
    } else if(modid == 1.) {
      return vec3(.5, 0.1, 0.);
    } else if(modid == 2.) {
      return vec3(.5, 0.0, 0.5);
    } else if(modid == 3.) {
      return vec3(.0, 0.5, 0.5);
    } else if(modid == 4.) {
      return vec3(.4, 0.5, 0.);
    }
    return vec3(.5, 0., 0.);
  }
  
  Surface getSurface(int object_id, float rayDepth, vec3 sp) {
    return Surface(
      object_id, 
      rayDepth, 
      sp, 
      getObjectColour(object_id), 
      .5, 
      200.);
  }
  
  // Calculated the normal of any given point in space. Intended to be cast from the point of a surface
  vec3 calculate_normal(in vec3 position) {
    vec3 grad = vec3(
      world(vec3(position.x + eps, position.y, position.z)) - world(vec3(position.x - eps, position.y, position.z)),
      world(vec3(position.x, position.y + eps, position.z)) - world(vec3(position.x, position.y - eps, position.z)),
      world(vec3(position.x, position.y, position.z + eps)) - world(vec3(position.x, position.y, position.z - eps))
    );
    
    return normalize(grad);
  }
  
  // The raymarch loop
  Surface rayMarch(vec3 ro, vec3 rd, float start, float end, inout vec3 c) {
    float sceneDist = 1e4;
    float rayDepth = start;
    int object_id = 0;
    bool inside = false;
    for(int i = 0; i < maxIterations; i++) {
      vec3 norm = vec3(0);
      sceneDist = world(ro + rd * rayDepth, object_id, norm);
      
      if(rayDepth > end || length(c) >= 3.) {
        break;
      }
      
      if(sceneDist < stopThreshold && sceneDist > 0.) {
        // c += lighting(getSurface(object_id, rayDepth, ro + rd * rayDepth), ro);
        Surface o = getSurface(object_id, rayDepth, ro + rd * rayDepth);
        
        float sceneLength = length(ro - o.position);
        float sceneAttenuation = min( 1. / ( 0.15 * sceneLength ), 1. );
        c += (o.colour*o.colour*(norm*.5+1.5))*.5*sceneAttenuation;
        // rd += norm*.01;
        rd = normalize(refract(rd, norm, 1.005 + u_mouse.y));
        rayDepth += stopThreshold+.62;
      } else if(sceneDist < 0.) {
        c += .1;
        rayDepth += .1;
        // rayDepth += (1. / abs(sceneDist)) * .5;
      } else {
        rayDepth += sceneDist * stepScale;
      }
      
    }
    
    return getSurface(object_id, rayDepth, ro + rd * rayDepth);
  }

  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    // movement
    movement = path(u_time);
 //   movement = vec3(0);
    
    // Camera and look-at
    vec3 cam = vec3(0,0,-1);
    vec3 lookAt = vec3(sin(u_time)*.2+.2,cos(u_time)*.01-.4,0);
    lookAt=vec3(0);
    
    // add movement
    lookAt += movement;
    cam += movement;
    
    // Unit vectors
    vec3 forward = normalize(lookAt - cam);
    vec3 right = normalize(vec3(forward.z, 0., -forward.x));
    vec3 up = normalize(cross(forward, right));
    
    // FOV
    float FOV = .6;
    
    // Ray origin and ray direction
    vec3 ro = cam;
    vec3 rd = normalize(forward + FOV * uv.x * right + FOV * uv.y * up);
    
    float t = movement.x * -.5;
    float s = sin(t);
    float c = cos(t);
    rd.xy *= mat2(c, -s, s, c);
    
    // Ray marching
    const float clipNear = 0.;
    const float clipFar = 32.;
    vec3 sceneColour = vec3(0.);
    rayMarch(ro, rd, clipNear, clipFar, sceneColour);
//     if(objectSurface.distance > clipFar) {
//       gl_FragColor = vec4(clipColour, 1.);
//       return;
//     }
    
//     vec3 sceneColour = lighting(objectSurface, cam);
    // vec3 sceneColour = vec3(dist*.1);
    
    gl_FragColor = vec4(sceneColour, 1.);
  }
  
</script>
<!-- partial -->
  <script src='https://codepen.io/shubniggurath/pen/aPxLMx.js'></script><script  src="./script.js"></script>

</body>
</html>
