<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Psychedelic Jellyfish Shader</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas" />

<script type="x-shader" id="shader_vertex">
#define PI 3.1415926535897932384626433832795

float TAU = PI * 2.0;

uniform float u_time;
uniform float u_width;
uniform float u_bump_frequency;
uniform float u_bump_scale;

varying vec3 v_pos;
varying vec3 v_orig_pos;
varying vec3 v_view_pos;
varying float v_bumps;

const float bump_iterations = 7.0;

void main() {

  
  vec4 model_position = modelMatrix * vec4(position, 1.0);

  vec3 pos = position;

  pos.xz *= u_width - (0.05 + smoothstep(0.0, -0.3, position.y) * -0.3) * u_width * 0.3;
  
  pos.xz *= 1.0 - sin((pos.y + u_time * 0.2 * TAU) * 2.0) * 0.2;
  pos.y *= 1.0 - sin((pos.y + (u_time + 0.3) * 0.2 * TAU) * 2.0) * .6;
  
  float bumps = 0.0;

  for (float i = 1.0; i < bump_iterations; i += 1.0) {
    float bump_scalar = (pos.y * 0.5 - 1.0) * u_bump_frequency * pow(i, 1.4);
    float bump_noise = snoise4(vec4(
      pos.x * bump_scalar,
      pos.y * bump_scalar + u_time * 0.1,
      pos.z * bump_scalar,
      u_time * 0.1 * pow(i, 1.0)
    ));
    bump_noise = abs(bump_noise);
    bumps += bump_noise * u_bump_scale / i * sin(((pos.y + u_time * 0.2 * TAU) - PI * .5) * 2.0);
  }
  pos *= 1.0 + bumps;


  pos.xz *= 1.0 - smoothstep(0.2, 0.1, position.y) * 0.35;

  model_position.xyz = pos;

  vec4 view_position = viewMatrix * model_position;
  vec4 projected_position = projectionMatrix * view_position;

  gl_Position = projected_position;

  v_pos = vec3(model_position.xyz);
  v_view_pos = vec3(view_position.xyz);
  v_bumps = bumps;
  v_orig_pos = position;
}
</script>

<script type="x-shader" id="shader_fragment">
uniform float u_time;

varying vec3 v_pos;
varying vec3 v_view_pos;
varying vec3 v_orig_pos;
varying float v_bumps;

void main() {

  vec3 color_dark = vec3(0, 0, 0);
  vec3 color_light = vec3(240, 230, 243);

  vec3 color = mix(color_dark, color_light, v_bumps * 0.002);

  color = mix(color, color_light, (1.5 + v_pos.y * 0.5) * 0.0008);

  float noise = snoise4(vec4(
    v_pos.x + v_orig_pos.y * 32.0,
    v_orig_pos.y * v_pos.y * 0.1,
    v_pos.z + v_orig_pos.y * 32.0,
    u_time
  ));
  noise = noise * 0.25 * smoothstep(0.1, 0.2, v_orig_pos.y);
  color -= noise;
  color.r *= 1.0 + distance(v_pos.xyz, vec3(0.0,v_orig_pos.y,0.0)) * 0.5;
  color.b += noise * v_pos.y;

  color.r *= 1.3;
  color.g *= v_pos.y + 0.5;
  color.b *= v_pos.y + 0.5;
  color.b += (1.0 - color.r) * 0.4;

  color *= .9;

  gl_FragColor = vec4(color, 1.0);
}
</script>

<script type="x-shader" id="shader_simplexNoise4D">
	//
// Description : Array and textureless GLSL 2D/3D/4D simplex
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0; }

float mod289(float x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0; }

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

float permute(float x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float taylorInvSqrt(float r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

vec4 grad4(float j, vec4 ip)
  {
  const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
  vec4 p,s;

  p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
  p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
  s = vec4(lessThan(p, vec4(0.0)));
  p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;

  return p;
  }

// (sqrt(5) - 1)/4 = F4, used once below
#define F4 0.309016994374947451

float snoise4(vec4 v)
  {
  const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                        0.276393202250021,  // 2 * G4
                        0.414589803375032,  // 3 * G4
                       -0.447213595499958); // -1 + 4 * G4

// First corner
  vec4 i  = floor(v + dot(v, vec4(F4)) );
  vec4 x0 = v -   i + dot(i, C.xxxx);

// Other corners

// Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
  vec4 i0;
  vec3 isX = step( x0.yzw, x0.xxx );
  vec3 isYZ = step( x0.zww, x0.yyz );
//  i0.x = dot( isX, vec3( 1.0 ) );
  i0.x = isX.x + isX.y + isX.z;
  i0.yzw = 1.0 - isX;
//  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
  i0.y += isYZ.x + isYZ.y;
  i0.zw += 1.0 - isYZ.xy;
  i0.z += isYZ.z;
  i0.w += 1.0 - isYZ.z;

  // i0 now contains the unique values 0,1,2,3 in each channel
  vec4 i3 = clamp( i0, 0.0, 1.0 );
  vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
  vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

  //  x0 = x0 - 0.0 + 0.0 * C.xxxx
  //  x1 = x0 - i1  + 1.0 * C.xxxx
  //  x2 = x0 - i2  + 2.0 * C.xxxx
  //  x3 = x0 - i3  + 3.0 * C.xxxx
  //  x4 = x0 - 1.0 + 4.0 * C.xxxx
  vec4 x1 = x0 - i1 + C.xxxx;
  vec4 x2 = x0 - i2 + C.yyyy;
  vec4 x3 = x0 - i3 + C.zzzz;
  vec4 x4 = x0 + C.wwww;

// Permutations
  i = mod289(i);
  float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
  vec4 j1 = permute( permute( permute( permute (
             i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
           + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
           + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
           + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

// Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
// 7*7*6 = 294, which is close to the ring size 17*17 = 289.
  vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

  vec4 p0 = grad4(j0,   ip);
  vec4 p1 = grad4(j1.x, ip);
  vec4 p2 = grad4(j1.y, ip);
  vec4 p3 = grad4(j1.z, ip);
  vec4 p4 = grad4(j1.w, ip);

// Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;
  p4 *= taylorInvSqrt(dot(p4,p4));

// Mix contributions from the five corners
  vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
  vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
  m0 = m0 * m0;
  m1 = m1 * m1;
  return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
               + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

  }


</script>
<!-- partial -->
  <script type="module" src="./script.js"></script>

</body>
</html>
