<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>Layered Noise | WebGL</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<!-- VertexShader code here -->
<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
precision highp float;
  in vec4 position;

  void main() {
    gl_Position = vec4( position );
  }
</script>

<!-- FragmentShader code here -->
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es

precision highp float;
out vec4 fragColor;
uniform vec2 u_resolution;
uniform vec4 u_mouse;
uniform float u_time;

#define R           u_resolution
#define T           u_time
#define M           u_mouse


#define PI  3.14159265359
#define PI2 6.28318530718


mat2 rot(float a) {return mat2(cos(a),sin(a),-sin(a),cos(a));}
float hash21(vec2 a){ return fract(sin(dot(a,vec2(22.54,35.45)))*4823.232);}

//@iq hsv
vec3 hsv( in vec3 c ) {
    vec3 rgb = clamp( abs(mod(c.x*2.+vec3(0,4,2),6.)-3.)-1., 0., 1.0 );
    return c.z * mix( vec3(1), rgb, c.y);
}

// @Shane - compact self-contained version
// of IQ's 3D value noise function.

float n3D(vec3 p){
    const vec3 s = vec3(113, 57, 27);
    vec3 ip = floor(p); p -= ip; 
    vec4 h = vec4(0., s.yz, s.y + s.z) + dot(ip, s);
    p = p*p*(3. - 2.*p);
    h = mix(fract(sin(h)*43758.5453), fract(sin(h + s.x)*43758.5453), p.x);
    h.xy = mix(h.xz, h.yw, p.y);
    return mix(h.x, h.y, p.z); // Range: [0, 1].
}
// @iq fbm generator
float fbm3( in vec3 x) {
    x*=.1;
    float a = 0.1,b = .5,f = 2.;
    vec3  d = vec3(0.);
    for (int i = 0; i < 4; ++i) {      
        float n = n3D(f*x);
        a += b*n;
        b *= 0.5;
        f *= 1.8;
    }
	return a;
}

void main() {
	  vec2 F = gl_FragCoord.xy;
    vec2 uv = (2.*F.xy-R.xy)/max(R.x,R.y); 
    vec2 vv = uv;

    float px = fwidth(uv.x); 
    vec3 C = vec3(.005);
  
    //camera
    float x = M.xy==vec2(0) || M.z<0. ? .0 : -(M.y / R.y * .4 - .2) * PI;
    float y = M.xy==vec2(0) || M.z<0. ? .0 :  (M.x / R.x * .4 - .2) * PI;
  
    vv *= 920.;
    float fx = mod(floor(T*20.),64.);
    for(float i=1.;i<128.;i++) {

        vec3 h = mod(i,2.)<1.? vec3( (i*.0025) ):hsv(vec3((i*.075),.95,(i*.008)));
        if (mod(i,64.)==fx) h=vec3((i*.009));
        if(i==127.)h=vec3(.01);
        vv *= .979;
        vv += vec2(y,x);
        float ff= (i*.165);
        float d = fbm3(vec3(vv*.25+vec2(T*.78,0),ff));

        if (d>.59) C = h;
    }

    // gamma and output
    C = pow(clamp(C,vec3(0),vec3(1)),vec3(.4545));
    fragColor = vec4(C,1.);
}

</script>
<!-- partial -->
  <script src='https://jhancock532.github.io/twgl.min.js'></script><script  src="./script.js"></script>

</body>
</html>
