<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - [WebGL] Emerald Noise</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<script id="vertexShader" type="x-shader/x-vertex">
	void main()	{
		gl_Position = vec4( position, 1.0 );
	}
</script>
<script id="fragmentShader" type="x-shader/x-fragment">

uniform float time;
uniform vec2 resolution;

#define PI 3.14159265359

// snoise function (C) 2011 Ashima Arts
// used under MIT License. see https://github.com/ashima/webgl-noise
vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 permute(vec4 x) {
  return mod289(((x*34.0)+1.0)*x);
}
vec4 taylorInvSqrt(vec4 r) {
  return 1.79284291400159 - 0.85373472095314 * r;
}
float snoise(vec3 v) { 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

  // First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

  // Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

  // Permutations
  i = mod289(i); 
  vec4 p = permute( permute( permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

  // Gradients: 7x7 points over a square, mapped onto an octahedron.
  // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

	//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

  // Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
}
// Rough gradient interpolator
vec3 palette(float n) {
	return (n < 1./3.) ? mix(vec3(0.,0.,0.), vec3(.02745,.02745,.25882), fract(n*3.))
  	: (n < 1./2.) ? mix(vec3(.02745,.02745,.25882), vec3(.05098,0.,.72157), fract(n*6.))
  	: (n < 4./6.) ? mix(vec3(.05098,0.,.72157), vec3(.62745,.07059,.98431), fract(n*6.))
  	: (n < 5./6.) ? mix(vec3(.62745,.07059,.98431), vec3(.92157,.12941,1.), fract(n*6.))
    	: mix(vec3(.92157,.12941,1.), vec3(1.,.81176,1.), fract(n*6.));
}
void main( void ) {
	float p = 289.* fract(time/100.);
  float n = 0.65 * snoise(vec3(gl_FragCoord.xy / 128., p/2.5)) +
            0.55 * snoise(vec3(gl_FragCoord.xy / 64., p/2.2)) +
            0.05 * snoise(vec3(gl_FragCoord.xy / 4., p/1.8)) +
            0.05 * snoise(vec3(gl_FragCoord.xy / 1., p/1.2));
  gl_FragColor = vec4(
  	palette(
    	1.6 * n * n
    ).gbr,
    1.0
  );
}
</script>
<div id="container"></div>
<div id="info">Emerald Noise - GLSL shader - with <a href="https://threejs.org" target="_blank">three.js</a>
</div>
<div id="controls">
  <label for="resolution">resolution: </label>
  <select id="resolution" value="2">
    <option value="0.5">0.5x</option>
    <option value="1">1x</option>
    <option value="2" selected>2x</option>
    <option value="4">4x</option>
    <option value="8">8x</option>
  </select>
</div>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/84/three.min.js'></script>
<script src='https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js'></script><script  src="./script.js"></script>

</body>
</html>
