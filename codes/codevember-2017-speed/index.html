<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - #Codevember 2017 — Speed</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<div id="main">
	<div id="content">
		<div id="title">Speed<br>Traces</div>
		<div id="header">	  	
			<div id="baseline">
				<p id="date">2017/11/19</p>
				<h1>#Codevember 17</h1>
				<p id="description">GLSL fragment experiment<br>made for #codevember 2017.<br>The theme of the day was "Speed".<b><em>Move your mouse to the center of the canvas to slow down time</em></b></p>
			</div>		  	
			<div id="author">— Code & Design by <a href="https://www.arivaux.com" target = "_blank">Alexandre Rivaux</a></div>
		</div>
	</div>
	<div id="container"></div>	
</div>

<!-- GLSL SCRIPT -->
<!-- vertex shader -->
<script id="vertexShader" type="x-shader/x-vertex">
	void main() {
		gl_Position = vec4(position, 1.0);
	}
</script>

<!-- fragment shader -->
<script id="fragmentShader" type="x-shader/x-fragment">
		uniform vec2 resolution;
		uniform float time;
		uniform vec2 mouse;
	
		#define TWO_PI 6.28318530718
	
		mat2 rotate2d(float _angle){
			return mat2(cos(_angle),-sin(_angle),
						sin(_angle),cos(_angle));
		}

		vec2 rotateFrom(vec2 uv, vec2 center, float angle){
			vec2 uv_ = uv - center;
			uv_ =  rotate2d(angle) * uv_;
			uv_ = uv_ + center;

			return uv_;
		}
		
		float random(float value){
			return fract(sin(value) * 43758.5453123);
		}

		float random(vec2 tex){
			return fract(sin(dot(tex.xy, vec2(12.9898, 78.233))) * 43758.5453123);
		}

		vec2 random2D(vec2 uv){
			uv = vec2(dot(uv, vec2(127.1, 311.7)), dot(uv, vec2(269.5, 183.3)));
			return -1.0 + 2.0 * fract(sin(uv) * 43758.5453123);
		}

		vec3 random3D(vec3 uv){
			uv = vec3(dot(uv, vec3(127.1, 311.7, 120.9898)), dot(uv, vec3(269.5, 183.3, 150.457)), dot(uv, vec3(380.5, 182.3, 170.457)));
			return -1.0 + 2.0 * fract(sin(uv) * 43758.5453123);
		}
	
		float cubicCurve(float value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		vec2 cubicCurve(vec2 value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		vec3 cubicCurve(vec3 value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		float noise(vec2 uv){
			vec2 iuv = floor(uv);
			vec2 fuv = fract(uv);
			vec2 suv = cubicCurve(fuv);

			float dotAA_ = dot(random2D(iuv + vec2(0.0)), fuv - vec2(0.0));
			float dotBB_ = dot(random2D(iuv + vec2(1.0, 0.0)), fuv - vec2(1.0, 0.0));
			float dotCC_ = dot(random2D(iuv + vec2(0.0, 1.0)), fuv - vec2(0.0, 1.0));
			float dotDD_ = dot(random2D(iuv + vec2(1.0, 1.0)), fuv - vec2(1.0, 1.0));

			return mix(
				mix(dotAA_, dotBB_,	suv.x),
				mix(dotCC_, dotDD_, suv.x),
				suv.y);
		}

		float noise(vec3 uv){
			vec3 iuv = floor(uv);
			vec3 fuv = fract(uv);
			vec3 suv = cubicCurve(fuv);

			float dotAA_ = dot(random3D(iuv + vec3(0.0)), fuv - vec3(0.0));
			float dotBB_ = dot(random3D(iuv + vec3(1.0, 0.0, 0.0)), fuv - vec3(1.0, 0.0, 0.0));
			float dotCC_ = dot(random3D(iuv + vec3(0.0, 1.0, 0.0)), fuv - vec3(0.0, 1.0, 0.0));
			float dotDD_ = dot(random3D(iuv + vec3(1.0, 1.0, 0.0)), fuv - vec3(1.0, 1.0, 0.0));

			float dotEE_ = dot(random3D(iuv + vec3(0.0, 0.0, 1.0)), fuv - vec3(0.0, 0.0, 1.0));
			float dotFF_ = dot(random3D(iuv + vec3(1.0, 0.0, 1.0)), fuv - vec3(1.0, 0.0, 1.0));
			float dotGG_ = dot(random3D(iuv + vec3(0.0, 1.0, 1.0)), fuv - vec3(0.0, 1.0, 1.0));
			float dotHH_ = dot(random3D(iuv + vec3(1.0, 1.0, 1.0)), fuv - vec3(1.0, 1.0, 1.0));

			float passH0 = mix(
				mix(dotAA_, dotBB_,	suv.x),
				mix(dotCC_, dotDD_, suv.x),
				suv.y);

			float passH1 = mix(
				mix(dotEE_, dotFF_,	suv.x),
				mix(dotGG_, dotHH_, suv.x),
				suv.y);

			return mix(passH0, passH1, suv.z);
		}
	
		float rect(vec2 uv, vec2 length, float smooth){
			float dx = abs(uv.x - 0.5);
			float dy = abs(uv.y - 0.5);
			float lenx = 1.0 - smoothstep(length.x - smooth, length.x + smooth, dx);
			float leny = 1.0 - smoothstep(length.y - smooth, length.y + smooth, dy);
			
			return lenx * leny;
		}
	
		vec4 addGrain(vec2 uv, float time, float grainIntensity){
    		float grain = random(fract(uv * time)) * grainIntensity;
    		return vec4(vec3(grain), 1.0);
		}

		void main(){
			vec2 uv = gl_FragCoord.xy / resolution.xy;
			
			float mouseOffset = length(vec2(0.5) - mouse) * 0.5;
			
			#define OCTAVES 6
			float speedOffset = 1.75;// + mousex;
			float trembleOffset = random(time) * 0.035;
			
			vec2 splitRGB = vec2(0.005, 0.01);
			
			vec3 color1 = vec3(1.0, 0.1, 0.1);
			vec3 color2 = vec3(0.1, 0.1, 1.0);
			vec3 backgroundColor = vec3(0.005, 0.0, 0.05);
			vec3 color = vec3(0.0);
			
			for(int i=1; i<OCTAVES; i++){
				
				float eta = (noise(vec3(time * float(i))) * 0.15 + 0.15) * 0.1;
				vec2 ruv = rotateFrom(uv, vec2(1.0), eta + TWO_PI * 0.125);
				
				float opacity = float(i) / float(OCTAVES);
				vec2 nuv = ruv * vec2(1.0, 80.0 / float(i));
				vec2 iuv = floor(nuv);
				vec2 fuv = fract(nuv);
				
				vec2 offset = vec2(random(vec2(iuv.y, float(i))), 0.0) * 2.0 - 1.0;
				float minSpeed = 1.0 * float(i) * speedOffset;
				float maxSpeed = 2.0 * float(i) * speedOffset;
				float speed = minSpeed + random(vec2(iuv.y * 2.5, float(i))) * (maxSpeed - minSpeed);
				vec2 animation = vec2(time * speed, 0.0) * mouseOffset;
				float minLength = 0.025;
				float maxLength = 0.15;
				float len = minLength + random(vec2(iuv.y * 5.0, float(i))) * (maxLength - minLength);
				float minThick = 0.25;
				float maxThick = 0.3;
				float thick = minThick + random(vec2(iuv.y * 10.0, float(i))) * (maxThick - minThick);
				vec2 tremble = vec2(0.0, trembleOffset * float(i));
				float randIndex =  random(vec2(iuv.y, float(i)));
				randIndex = step(0.15, randIndex);
				
				float randColorIndex = random(vec2(iuv.y * 20.0, float(i * i)));
				vec3 lineColor = mix(color1, color2, randColorIndex);
				
				float recR = rect(fract(tremble + fuv + splitRGB + offset + animation), vec2(len, maxThick), 0.01) * randIndex * opacity;
				float recG = rect(fract(tremble + fuv + offset + animation), vec2(len, maxThick), 0.01) * randIndex * opacity;
				float recB = rect(fract(tremble + fuv - splitRGB + offset + animation), vec2(len, maxThick), 0.01) * randIndex * opacity;
				
				color += lineColor * vec3(recR, recG, recB);
			}
			
			vec4 grain = addGrain(uv, time, 0.1);
			
			gl_FragColor = vec4(backgroundColor + color, 1.0) + grain;
		}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/87/three.min.js'></script>
<script src='https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js'></script><script  src="./script.js"></script>

</body>
</html>
