<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - #curiouslyminded Ray Marching</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<!-- Shader Sessions | 2022
 *
 * We're curiouslyminded:
 * https://www.curiouslyminded.xyz
 * https://www.twitch.tv/curiouslyminded
 * https://www.youtube.com/curiouslyminded -->

<div id="shadercollab"></div>
<script id="vertex" type="x-shader/x-vertex">
	void main() { gl_Position = vec4(position, 1.0); }
</script>

<script id="fragment" type="x-shader/x-fragment">
precision highp float;

#define PI 3.14159265
#define TAU 6.2831

uniform vec2 u_resolution;
uniform float u_time;
uniform vec2 u_mouse;
	   
const int RAYMARCH_MAX_STEPS = 100;
const float RAYMARCH_MAX_DIST = 10.;
const float EPSILON = 0.0001; 
	
// cosPallete
// a - brigthness, b - contrast, c - osc, d - phase
vec3 palette(in vec3 t, in vec3 a, in vec3 b, in vec3 c, in vec3 d) { 
	return a + b*cos( 6.28318*(c*t+d) ); 
}

mat2 Rot(float a) {
    float s=sin(a), c=cos(a);
    return mat2(c, -s, s, c);
}

float sdBox(vec3 p, vec3 s) {
    p = abs(p)-s;
   return length(max(p, 0.))+min(max(p.x, max(p.y, p.z)), 0.);
}

float GetDist(vec3 p, vec2 mouse) { 
	vec3 mouseRotation = p;
	mouseRotation.yz *= Rot(-mouse.y * PI + 1.);
	mouseRotation.xz *= Rot(-mouse.x * TAU);
	
    float d = sdBox(mouseRotation, vec3(1.));    

    return d;
}

// Getting a direction/normal for the light
vec3 GetNormal(vec3 p, vec2 mouse) {
	float d = GetDist(p, mouse);
	vec2 e = vec2(sin(u_time + 0.006), 0.);
    
    vec3 n = d - vec3(
        GetDist(p-e.xyy, mouse),
        GetDist(p-e.yxy, mouse),
        GetDist(p-e.yyx, mouse)
	);
    
    return normalize(n);
}

float RayMarch(vec3 ro, vec3 rd, float side, vec2 mouse) {
   float dO = 0.0;
    
    for(int i=0; i < RAYMARCH_MAX_STEPS; i++) {
      vec3 p = ro + rd * dO;
        float dS = GetDist(p, mouse) * side;
        dO += dS;
        if(dO > RAYMARCH_MAX_DIST || abs(dS) < EPSILON) break;
    }
    
    return dO;
}
	
void main() {
	vec2 uv = (gl_FragCoord.xy - u_resolution * .5) / u_resolution.yy; 
	vec2 m = u_mouse.xy/u_resolution.xy;
	
	vec3 camPos = vec3(0.0, 0.0, -5.); // x, y, z axis	
	vec3 rayDir = normalize(vec3(uv, 1.)); // DOF
	
	vec3 col = vec3(0.0);
	
	float d = RayMarch(camPos, rayDir, 1.0, m);

	
	float IOR = 1.9; // Index of refraction
    if (d < RAYMARCH_MAX_DIST) {
        vec3 p = camPos + rayDir * d; 
        vec3 n = GetNormal(p, m); 
 
		vec3 rdIn = refract(rayDir, n, 1.0/IOR); 
        
         vec3 pEnter = p - n * EPSILON * 3.0;
		 float dIn = RayMarch(pEnter, rdIn, -1.0, m);  

         vec3 pExit = pEnter + rdIn * dIn; 
         vec3 nExit = -GetNormal(pExit, m);
        
         vec3 rdOut = refract(rdIn, nExit, IOR); 
        
         if (dot(rdOut, rdOut) == 0.0) {
			 rdOut = reflect(rdIn, nExit);
		 }

        vec3 reflCol = palette(
			vec3(rdOut), 
			vec3(0.5), 
			vec3(.5), 
			vec3(1.45, .15, .65), 
			vec3(.97, .23, .62)
		);
		
		col = reflCol;
		
	}
	
	col = pow(col, vec3(.4535)); // gamma correction
	
    gl_FragColor = vec4(col, 1.0);
}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/r99/three.min.js'></script><script  src="./script.js"></script>

</body>
</html>
