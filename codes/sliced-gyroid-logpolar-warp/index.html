<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>Sliced Gyroid LogPolar Warp</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<!-- VertexShader code here -->
<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
precision highp float;
  in vec4 position;

  void main() {
    gl_Position = vec4( position );
  }
</script>

<!-- FragmentShader code here -->
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
precision highp float;
out vec4 fragColor;
uniform vec2 u_resolution;
uniform vec4 u_mouse;
uniform float u_time;

#define R           u_resolution
#define T           u_time
#define M           u_mouse
#define PI    3.14159265358
#define PI2   6.28318530718

mat2 rot (float a) { return mat2(cos(a),sin(a),-sin(a),cos(a)); }
float hash21(vec2 a){ return fract(sin(dot(a,vec2(22.34,35.34)))*483434.);}

//globals
mat2 trot;
float glow=0.,tglow=0.;

const float ofx = 1.25;

float gyroid(vec3 p, float s, float t, float b) {
    p *=s;
    float g = abs(dot(sin(p*ofx), cos(p.zxy))-b)/(s*ofx)-t;
    return g;
}

vec2 map(vec3 p) {
    vec2 res =vec2(1e5,1.);
  
    float k = 8./dot(p,p);
    float mul = 1./k;
    p*=k;

    p.xy *= trot;
    p.yz += vec2(-1.25, T);
    
    float mm = 3.85+3.75*sin(T*.15);
    float ms = 1.85+1.75*cos(T*.35);
    
    float d = .35, mf = 1e5;
    for(float j=-1.;j<2.;j++){
        vec3 nf =p;
        nf.z=round(nf.z/d+j)*d;
 
        float ids = mod(nf.z,4.);
        
        float idx = mod(nf.z,8.);
        float fd = gyroid(nf, .75, .0, .525);

        fd=abs(fd)-.025;
        nf.z=clamp(p.z,nf.z-d/2.,nf.z+d/2.);
        fd=length(vec2(max(.0,fd), nf.z-p.z));

        
        if(idx<mm+.15&&idx>mm) tglow+=.002/(.015+fd*fd);
        if(ids<ms+.15&&ids>ms) glow+=.002/(.015+fd*fd);
   
        mf=min(mf,fd);
    }
    
    if(mf<res.x) res=vec2(mf,3.);

    res.x*= mul/1.35;
    return res;
}

void main() {
	  vec2 uv = (2.*gl_FragCoord.xy.xy - R.xy)/max(R.x,R.y);

    trot=rot(T*.072);
    
    vec3 ro = vec3(0,0,.5);
    vec3 rd = normalize(vec3(uv, -1));
    
    float x = M.xy == vec2(0) || M.z < 1. ? 0. : (M.y/R.y * .5-.25) * PI;
    float y = M.xy == vec2(0) || M.z < 1. ? 0. :-(M.x/R.x * .5-.25) * PI;

    mat2 rx = rot(x),ry = rot(y);
    
    ro.yz *= rx;ro.xz *= ry;
    rd.yz *= rx;rd.xz *= ry;
    
    vec3 C = vec3(0);
    vec3 p = ro;

    float d=0.,m=0.;
    for(int i=0;i<164;i++){
        vec2 t = map(p);
        d += i<64? t.x*.35:t.x*.75;
        m  = t.y;  
        p = ro + rd * d;
        if(t.x<d*1e-4||d>45.) break;
    } 
    
    float sp = .2+.2*sin(uv.x*4.1+T);
    vec3 fog = mix(vec3(0.043,0.153,0.255),vec3(0.235,0.302,0.000),clamp((uv.y+.5-sp),0.,1.));

    C = mix(C,vec3(glow*.65,glow,glow*.15),clamp(glow,.0,.6));
    C = mix(C,vec3(tglow*.15,tglow*.465,tglow),clamp(tglow,.0,.8));
    
    C = mix(C,fog, 1.-exp(-20.*d*d*d));
    
    C = clamp(C,vec3(0),vec3(1));
    C = pow(C,vec3(.4545));
 	  fragColor = vec4(C,1.);
}
</script>
<!-- partial -->
  <script src='https://jhancock532.github.io/twgl.min.js'></script><script  src="./script.js"></script>

</body>
</html>
