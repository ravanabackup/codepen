<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - #curiouslyminded â€” 15_LiveShaderCodingSession</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<!-- Shader Collab
 * By eliza & ilithya | 2021
 *
 * We're curiouslyminded:
 * https://www.curiouslyminded.xyz
 * https://www.twitch.tv/curiouslyminded
 * https://www.youtube.com/curiouslyminded -->

<div id="shadercollab"></div>
<script id="vertex" type="x-shader/x-vertex">
	void main() { gl_Position = vec4(position, 1.0); }
</script>

<script id="fragment" type="x-shader/x-fragment">
precision highp float;

uniform vec2 u_resolution;
uniform float u_time;
	
const int RAYMARCH_MAX_STEPS = 200;
const float RAYMARCH_MAX_DIST = 10.;
const float EPSILON = 0.0001;
	
// Got rotation functions from akella - twitter.com/akella
mat4 rotationMatrix(vec3 axis, float angle) {
    axis = normalize(axis);
    float s = sin(angle);
    float c = cos(angle);
    float oc = 1.0 - c;
    
    return mat4(oc * axis.x * axis.x + c, 
				oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           
				oc * axis.y * axis.z - axis.x * s,  0.0,
                oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           
				0.0,
                0.0,                                
				0.0,                                
				0.0,                                
				1.0);
}

vec3 rotate(vec3 v, vec3 axis, float angle) {
	mat4 m = rotationMatrix(axis, angle);
	return (m * vec4(v, 1.0)).xyz;
}

// Torus function by Inigo Quilez - iquilezles.org
// p - position, t - size
float sdTorus(vec3 p, vec2 t) {
	vec2 q = vec2(length(p.xz)-t.x,p.y);
	return length(q)-t.y;
}
	
float scene(vec3 pos) {
	vec3 rotation = rotate(
		vec3(pos.x, pos.y, pos.z+1.5), 
		vec3(1.), 
		2.
	);	

 	vec3 wPos = rotation; // warp position
 	const float warpScale = 1.5;
		
 	// Cosine domain warp
 	wPos += warpScale * 0.10000 * cos(3. * wPos.yzx + u_time);
 	wPos += warpScale * 0.20000 * cos(5. * wPos.zzx + u_time);
 	wPos += warpScale * 0.52500 * cos(1. * wPos.yyz + u_time);
	
 	rotation = wPos;
	
	float size = 0.5;	
	float torus = sdTorus(rotation, vec2(size, size*0.5));
	
	return torus;
}

vec3 background(vec3 rayDir) {	
	vec2 nUV = vec2(rayDir.x, rayDir.y);

	vec3 c1 = vec3(0.5, 0.0 ,0.5); // purple
	vec3 c2 = vec3(0.0);
	vec3 c3 = vec3(1., 1., 0.); // yellow
	
	vec3 color = c2;

	color += 0.25 - fract(nUV.x * 5.);
	color += 0.5 - fract(nUV.y * 5. + u_time);
	
	color *= mix(c1, c3, sin(u_time) * 0.5);
		
	return color;
}
	
vec4 raymarch(vec3 rayDir, vec3 pos) {
	float currentDist = 0.0;
	float rayDepth = 0.0;
	vec3 rayLength = vec3(0.0);
	
	vec4 bgColor = vec4(background(rayDir), 1.0);
	
	// shooting the ray
 	for (int i=0; i < RAYMARCH_MAX_STEPS; i++) {
     	// steps traveled
		currentDist = scene(pos + rayDir * rayDepth);
		rayDepth += currentDist;

		vec3 c1 = vec3(1., 0.4, 0.8);
		vec3 c2 = vec3(0.0, 0.5, 1.0);
		
		vec3 color = mix(
			c1, 
			c2 * rayDepth, 
			rayDir.x + rayDir.y + sin(u_time)
		);
		
 		vec4 shapeColor = vec4(color, 1.0);
		
 	    if (currentDist < EPSILON) return shapeColor; // We're inside the scene - magic happens here
 		if (rayDepth > RAYMARCH_MAX_DIST) return bgColor; // We've gone too far
	}
	
	return bgColor;
}
	
void main() {
	vec2 uv = (gl_FragCoord.xy - u_resolution * .5) / u_resolution.yy; 
	
	vec3 camPos = vec3(0.0, 0.0, 0.1); // x, y, z axis	
	float z = mix(0.3, 0.7, abs(.8));
	vec3 rayDir = normalize(vec3(uv, -z)); // DOF
	
    gl_FragColor = vec4(raymarch(rayDir, camPos));
}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/r128/three.min.js'></script><script  src="./script.js"></script>

</body>
</html>
