<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - The Temple</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="webgl" width="500" height="1758"></canvas>

<script id="vertexShader" type="x-shader/x-vertex">
  attribute vec4 a_position;
  
  uniform mat4 u_modelViewMatrix;
  uniform mat4 u_projectionMatrix;
  
  void main() {
    gl_Position = a_position;
  }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
 precision highp float;
  precision highp int;
  
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  
  // movement variables
  vec3 movement = vec3(.0);
  
  const int maxIterations = 256;
  const float stopThreshold = 0.001;
  const float stepScale = .4;
  const float eps = 0.005;
  const vec3 clipColour = vec3(0.);
  const vec3 fogColour = vec3(0.);
  
  const vec3 light1_position = vec3(0, 1., -1.);
  const vec3 light1_colour = vec3(.8, .8, .85);
  
  struct Surface {
    int object_id;
    float distance;
    vec3 position;
    vec3 colour;
    float ambient;
    float spec;
  };
  float sdCappedCylinder( vec3 p, float h, float r ) {
    vec2 d = abs(vec2(length(p.xz),p.y)) - vec2(h,r);
    return min(max(d.x,d.y),0.0) + length(max(d,0.0));
  }
  
  // This function describes the world in distances from any given 3 dimensional point in space
  float world(in vec3 position, inout int object_id) {
    
    // float s = sin(position.z*.1);
    // float c = cos(position.z*.1);
    // mat2 rot = mat2(c, -s, s, c);
    // position.xy *= rot;
    
    object_id=1;
    float world = 100.;
    
    vec3 p = position;
    p.z = mod(p.z, 1.) - .5;
    if(p.x > -1.5) {
      p.x -= 1.5;
      p.x = mod(p.x, 3.) - 1.5;
    }
    vec2 q = vec2(length(p.xz), atan(p.x, p.z) );
    float pillar = length( q.x + clamp(abs(sin(q.y * 8.)), 0.5, 1.) * .02) - .25;
    pillar = min(pillar, sdCappedCylinder(p + vec3(0, .5, 0), .28, .2));
    world = min(world, pillar);
    
    float floor = position.y + .5;
    world = min(world, floor);
    
    return world;
  }
  float world(in vec3 position) {
    int dummy = 0;
    return world(position, dummy);
  }
  
  vec3 getObjectColour(int object_id) {
    float modid = mod(float(object_id), 5.);
    return vec3(.7);
  }
  
  Surface getSurface(int object_id, float rayDepth, vec3 sp) {
    return Surface(
      object_id, 
      rayDepth, 
      sp, 
      getObjectColour(object_id), 
      .5, 
      200.);
  }
  
  // Calculated the normal of any given point in space. Intended to be cast from the point of a surface
  vec3 calculate_normal(in vec3 position) {
    vec3 grad = vec3(
      world(vec3(position.x + eps, position.y, position.z)) - world(vec3(position.x - eps, position.y, position.z)),
      world(vec3(position.x, position.y + eps, position.z)) - world(vec3(position.x, position.y - eps, position.z)),
      world(vec3(position.x, position.y, position.z + eps)) - world(vec3(position.x, position.y, position.z - eps))
    );
    
    return normalize(grad);
  }
  
  // The raymarch loop
  Surface rayMarch(vec3 ro, vec3 rd, float start, float end, inout float field) {
    float sceneDist = 1e4;
    float rayDepth = start;
    int object_id = 0;
    for(int i = 0; i < maxIterations; i++) {
      sceneDist = world(ro + rd * rayDepth, object_id);
      
      field += clamp(1./sceneDist, 0., .2);
      
      if(sceneDist < stopThreshold || rayDepth > end) {
        break;
      }
      
      rayDepth += sceneDist * stepScale;
    }
    
    return getSurface(object_id, rayDepth, ro + rd * rayDepth);
  }
  
  float calcSoftshadow( in vec3 ro, in vec3 rd, in float mint, in float tmax, inout float lightfield ) {
    float res = 1.0;
      float t = mint;
      float ph = 1e10; // big, such that y = 0 on the first iteration

      for( int i=0; i<64; i++ ) {
        float h = world( ro + rd*t );
        float y = h*h/(2.0*ph);
        float d = sqrt(h*h-y*y);
        res = min( res, 10.0*d/max(0.0,t-y) );
        ph = h;

        t += h;
        
        lightfield += .1;

        if( res<0.0001 || t>tmax ) break;

      }
      return clamp( res, 0.0, 1.0 );
  }
  
  vec3 lighting(Surface surface_object, vec3 cam, float field, inout float lightfield) {
    
    // start with black
    vec3 sceneColour = vec3(0);
    
    // Surface normal
    vec3 normal = calculate_normal(surface_object.position);
    
    // Light position
    vec3 lp = cam+vec3(-5, 10., -2.);
    // Light direction
    vec3 ld = lp - surface_object.position;
    
    // light attenuation
    // For brightly lit scenes or global illumination (like sunlit), this can be limited to just normalizing the ld
    float len = length( ld );
    ld = normalize(ld);
    float lightAtten = min( 1.0 / ( 0.15*len ), 1.0 );
    lightAtten = 1.;
    
    // Scene values, mainly for fog
    float sceneLength = length(cam - surface_object.position);
    float sceneAttenuation = min( 1. / ( 0.015 * sceneLength * sceneLength ), 1. );
    
    // The surface's light reflection normal
    vec3 reflection_normal = reflect(-ld, normal);
    
    // Ambient Occlusion
    float ao = 1.;
    
    float shadows = calcSoftshadow( surface_object.position, ld, .00001, 100., lightfield );
    
    // Object surface properties
    float diffuse = max(0., dot(normal, ld));
    float specular = max(0., dot( reflection_normal, normalize(cam - surface_object.position) ));
    
    // Bringing all of the lighting components together
    sceneColour += ( clamp(1.-field*.05, 0., 1.) * (diffuse + surface_object.ambient) + specular ) * light1_colour * lightAtten;
    // adding fog
    sceneColour = mix( sceneColour, fogColour, 1. - sceneAttenuation );
    
    return sceneColour * clamp(shadows+.2, 0., 1.);
  }
  
  vec3 path(float z) {
    // return vec3(0,0,0.);
    return vec3(sin(z)*.2+.2,abs(sin((-1000.+z)*15.))*.031,0.+z);
  }

  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    // movement
    movement = path(u_time);
    
    // Camera and look-at
    vec3 cam = vec3(1,1,-2);
    vec3 lookAt = vec3(sin(clamp(cos(u_time*2.), 0., 1.)+u_time)*.5 + cos(u_time*3.)*.3,.05+cos(u_time*2.)*.2,0);
    if(length(u_mouse) > 0.) {
      lookAt.xy = u_mouse*2.;
    }
    
    // add movement
    lookAt += movement;
    cam += movement;
    
    // Unit vectors
    vec3 forward = normalize(lookAt - cam);
    vec3 right = normalize(vec3(forward.z, 0., -forward.x));
    vec3 up = normalize(cross(forward, right));
    
    // FOV
    float FOV = .8;
    
    // Ray origin and ray direction
    vec3 ro = cam;
    vec3 rd = normalize(forward + FOV * uv.x * right + FOV * uv.y * up);
    
    // Ray marching
    const float clipNear = 0.;
    const float clipFar = 32.;
    float field = 0.;
    Surface objectSurface = rayMarch(ro, rd, clipNear, clipFar, field);
    float n = texture2D(u_noise, (uv.xy + objectSurface.distance)*15.).r*.2;
    if(objectSurface.distance > clipFar) {
      gl_FragColor = vec4(vec3(n), 1.);
      return;
    }
    float lightfield = 0.;
    vec3 sceneColour = lighting(objectSurface, cam, field, lightfield);
    // sceneColour = vec3(field*.1);
    
    gl_FragColor = vec4(sceneColour + n, 1.);
  }
  
</script>
<!-- partial -->
  <script src='https://codepen.io/shubniggurath/pen/aPxLMx.js'></script><script  src="./script.js"></script>

</body>
</html>
