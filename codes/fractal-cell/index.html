<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Fractal Cell</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<div id="container"></div>	

<!-- GLSL SCRIPT -->
<!-- vertex shader -->
<script id="vertexShader" type="x-shader/x-vertex">
	void main() {
		gl_Position = vec4(position, 1.0);
	}
</script>

<!-- fragment shader -->
<script id="fragmentShader" type="x-shader/x-fragment">
		#define TWO_PI 6.28318530718
		uniform vec2 resolution;
		uniform float time;
		uniform vec2 colsrows;
	
		mat2 rotate2d(float _angle){
			return mat2(cos(_angle),-sin(_angle),
						sin(_angle),cos(_angle));
		}
		
		vec2 rotateFrom(vec2 uv, vec2 center, float angle){
			vec2 uv_ = uv - center;
			uv_ =  rotate2d(angle) * uv_;
			uv_ = uv_ + center;

			return uv_;
		}
		
		float random(float value){
			return fract(sin(value) * 43758.5453123);
		}
		
		float random(vec2 tex){
			return fract(sin(dot(tex.xy, vec2(12.9898, 78.233))) * 43758.5453123);
		}
	
		vec2 random2D(vec2 uv){
			uv = vec2(dot(uv, vec2(127.1, 311.7)), dot(uv, vec2(269.5, 183.3)));
			//return -1.0 + 2.0 * fract(sin(uv) * 43758.5453123);
			return fract(sin(uv) * 43758.5453123); //return without offset on x, y
		}

		vec3 random3D(vec3 uv){
			uv = vec3(dot(uv, vec3(127.1, 311.7, 120.9898)), dot(uv, vec3(269.5, 183.3, 150.457)), dot(uv, vec3(380.5, 182.3, 170.457)));
			return -1.0 + 2.0 * fract(sin(uv) * 43758.5453123);
		}
	
		float cubicCurve(float value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		vec2 cubicCurve(vec2 value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		vec3 cubicCurve(vec3 value){
			return value * value * (3.0 - 2.0 * value); // custom cubic curve
		}

		float noise(vec2 uv){
			vec2 iuv = floor(uv);
			vec2 fuv = fract(uv);
			vec2 suv = cubicCurve(fuv);

			float dotAA_ = dot(random2D(iuv + vec2(0.0)), fuv - vec2(0.0));
			float dotBB_ = dot(random2D(iuv + vec2(1.0, 0.0)), fuv - vec2(1.0, 0.0));
			float dotCC_ = dot(random2D(iuv + vec2(0.0, 1.0)), fuv - vec2(0.0, 1.0));
			float dotDD_ = dot(random2D(iuv + vec2(1.0, 1.0)), fuv - vec2(1.0, 1.0));

			return mix(
				mix(dotAA_, dotBB_,	suv.x),
				mix(dotCC_, dotDD_, suv.x),
				suv.y);
		}

		float noise(vec3 uv){
			vec3 iuv = floor(uv);
			vec3 fuv = fract(uv);
			vec3 suv = cubicCurve(fuv);

			float dotAA_ = dot(random3D(iuv + vec3(0.0)), fuv - vec3(0.0));
			float dotBB_ = dot(random3D(iuv + vec3(1.0, 0.0, 0.0)), fuv - vec3(1.0, 0.0, 0.0));
			float dotCC_ = dot(random3D(iuv + vec3(0.0, 1.0, 0.0)), fuv - vec3(0.0, 1.0, 0.0));
			float dotDD_ = dot(random3D(iuv + vec3(1.0, 1.0, 0.0)), fuv - vec3(1.0, 1.0, 0.0));

			float dotEE_ = dot(random3D(iuv + vec3(0.0, 0.0, 1.0)), fuv - vec3(0.0, 0.0, 1.0));
			float dotFF_ = dot(random3D(iuv + vec3(1.0, 0.0, 1.0)), fuv - vec3(1.0, 0.0, 1.0));
			float dotGG_ = dot(random3D(iuv + vec3(0.0, 1.0, 1.0)), fuv - vec3(0.0, 1.0, 1.0));
			float dotHH_ = dot(random3D(iuv + vec3(1.0, 1.0, 1.0)), fuv - vec3(1.0, 1.0, 1.0));

			float passH0 = mix(
				mix(dotAA_, dotBB_,	suv.x),
				mix(dotCC_, dotDD_, suv.x),
				suv.y);

			float passH1 = mix(
				mix(dotEE_, dotFF_,	suv.x),
				mix(dotGG_, dotHH_, suv.x),
				suv.y);

			return mix(passH0, passH1, suv.z);
		}
	
		float rect(vec2 uv, vec2 length, float smooth){
			float dx = abs(uv.x - 0.5);
			float dy = abs(uv.y - 0.5);
			float lenx = 1.0 - smoothstep(length.x - smooth, length.x + smooth, dx);
			float leny = 1.0 - smoothstep(length.y - smooth, length.y + smooth, dy);
			
			return lenx * leny;
		}
	
		vec4 addGrain(vec2 uv, float time, float grainIntensity){
    		float grain = random(fract(uv * time)) * grainIntensity;
    		return vec4(vec3(grain), 1.0);
		}
	
		vec2 fishey(vec2 uv, vec2 center, float ratio, float dist){
			  vec2 puv = uv + vec2(1.0);
			 //center coords
			  vec2 m = vec2(center.x, center.y/ratio) + vec2(1.0);
			  //vector from center to current fragment
			  vec2 d = puv - m;
			  // distance of pixel from center
			  float r = sqrt(dot(d, d)); 
			  //amount of effect
			  float power = ( TWO_PI / (2.0 * sqrt(dot(m, m)))) * mix(0.1, 0.4, pow(dist, 0.75));
			  //radius of 1:1 effect
			  float bind;
			  if (power > 0.0) bind = sqrt(dot(m, m));//stick to corners
			  //else {if (ratio < 1.0) bind = m.x; else bind = m.y;}//stick to borders

			  //Weird formulas
			  vec2 nuv;
			  if (power > 0.0)//fisheye
				nuv = m + normalize(d) * tan(r * power) * bind / tan( bind * power);
			  else if (power < 0.0)//antifisheye
			   nuv = m + normalize(d) * atan(r * -power * 10.0) * bind / atan(-power * bind * 10.0);
			  else 
				nuv = puv;//no effect for power = 1.0

			return nuv - vec2(1.0);
		}

		void main(){
			vec2 uv = gl_FragCoord.xy / resolution.xy;
			float ratio = resolution.x / resolution.y;
			
			uv = fishey(uv, vec2(0.5, 0.5/ratio), 1.0, 0.85);
			
			//animation
			float horizontalGlitch = sin(random(floor(uv.x * 100.0 + time)) * TWO_PI);
			float amp = noise(vec2(uv.y + time * horizontalGlitch));
			uv.y = fract(uv.y + horizontalGlitch * 0.005 * amp);
			
			uv = rotateFrom(uv, vec2(0.5, 0.5 * ratio), time * 0.01);
			uv.y += time*0.1;
			uv.y += floor(time);
			
			
			//cell
			float mod2 = 1.0 + mod(floor(uv.y * (colsrows.x / ratio)), 2.0);
			uv.x = fract(uv.x + 0.75 * mod2);
			vec2 nuv = uv * vec2(colsrows.x, colsrows.x / ratio);
			vec2 fuv = fract(nuv);
			vec2 iuv = floor(nuv);
			
			#define OCTAVE 8
			#define SUBDIV 2
			for(int i=0; i<OCTAVE; i++){
				float randRatio = random(iuv + vec2(random(floor(time)) * 0.1, 1.0 + floor(time)));
				float noiseRatio = sin(noise(vec3(iuv * 0.05, time)) * (TWO_PI * 0.5)) * 0.5;
				if(randRatio + noiseRatio > 0.5){
					nuv = fuv * vec2(SUBDIV);
					fuv = fract(nuv);
					iuv += floor(nuv + float(i));
				}
			}
			float index = random(iuv);
			float isLight = step(0.5, index);
			
			float randomSpeed = sin(floor(time + iuv.y) * 0.1 * TWO_PI) + index;
			float minSpeed = -2.0;
			float maxSpeed = 2.0;
			float speed = mix(minSpeed, maxSpeed, randomSpeed);
			float orientation = step(0.9, index);
			fuv.x = mix(fuv.x, fract(fuv.x + time * speed), orientation);
			fuv.y = mix(fract(fuv.y + time * speed), fuv.y, orientation);
			
			//shape
			float randIndex = random(iuv * 0.01 + floor(time));
			float minSize = 0.05;
			float maxSize = 0.35;
			float size = mix(minSize, maxSize, randIndex);
			
			float shiftNoiseAnimation = noise(vec2(iuv * time * 0.1)) * 0.25;
			float shiftRandomAnimation = random(vec2(time)) * 0.01;
			vec2 offset = vec2(shiftRandomAnimation + shiftNoiseAnimation, 0.0);
			float minLight = 0.05;
			float maxLight = 1.0;
			float light = mix(minLight, maxLight, index);
			float shapeRed = rect(fuv - offset, vec2(size), 0.01);
			float shapeGreen = rect(fuv, vec2(size), 0.01);
			float shapeBlue = rect(fuv + offset, vec2(size), 0.01);
			
			//color
			vec3 background = vec3(18.0/255.0, 14.0/255.0, 29.0/255.0);
			vec3 foreground0 = vec3(91.0/255.0, 47.0/255.0, 136.0/255.0);
			vec3 foreground1 = vec3(250.0/255.0, 219.0/255.0, 7.0/255.0);
			float colorIndex = step(0.85, random(iuv + floor(time)));
			vec3 foreground = mix(foreground0 * light, foreground1, colorIndex);
			vec3 mixer = vec3(shapeRed, shapeGreen, shapeBlue) * isLight;
			
			float randGrain = random(time * 0.001);
			vec4 grain = addGrain(uv, time, 0.05 + randGrain * 0.05);
			
			gl_FragColor = vec4(mix(background, foreground, mixer), 1.0) + grain;
		}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/87/three.min.js'></script><script  src="./script.js"></script>

</body>
</html>
