<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - GLSL: Cross in 3D</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="webgl" width="500" height="1758"></canvas>

<script id="vertexShader" type="x-shader/x-vertex">
  attribute vec4 a_position;
  
  uniform mat4 u_modelViewMatrix;
  uniform mat4 u_projectionMatrix;
  
  void main() {
    gl_Position = a_position;
  }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
 precision highp float;
  precision highp int;
  
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  
  // movement variables
  vec3 movement = vec3(.0);
  
  const int maxIterations = 256;
  const float stopThreshold = 0.001;
  const float stepScale = .7;
  const float eps = 0.002;
  const vec3 clipColour = vec3(0.);
  const vec3 fogColour = vec3(0.);
  
  const vec3 light1_position = vec3(0, 1., -1.);
  const vec3 light1_colour = vec3(.8, .8, .85);
  
  struct Surface {
    int object_id;
    float distance;
    vec3 position;
    vec3 colour;
    float ambient;
    float spec;
  };
  
  // ----------------------------------------------
  // 2D SDF functions courtesy Inigo Quilez
  // https://www.iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
  // ----------------------------------------------
  float sdBox( vec3 p, vec3 b ) {
    vec3 d = abs(p) - b;
    return length(max(d,0.0))
           + min(max(d.x,max(d.y,d.z)),0.0); // remove this line for an only partially signed sdf 
  }
  
  mat2 rotmouse;
  mat2 rot;
  
  // This function describes the world in distances from any given 3 dimensional point in space
  float world(in vec3 position, inout int object_id) {
    position.xz *= rot;
    vec3 pos = floor(position*5.);
    object_id = int(mod(floor(pos.x + pos.y + pos.z), 2.));
    pos = position;
    pos.xy *= rotmouse;
    
    float box = min(
        sdBox(position, vec3(.15, .15, .4)),
        min(
          sdBox(position.yzx, vec3(.15, .15, .4)),
          sdBox(position.zxy, vec3(.15, .15, .4))
        )
      );
    return 
      min( max(
        max(
          abs(
            abs(
              abs(
                abs(box+.1)-.025
              )-.02
            )-.02
          )-.005,
        -pos.y), 
       -(length(position) - .1)
      ), length(position) - .02);
    // position = mod(position, 1.) - .5;
    return length(position) - .3;
  }
  float world(in vec3 position) {
    int dummy = 0;
    return world(position, dummy);
  }
  
  vec3 getObjectColour(int object_id) {
    float modid = mod(float(object_id), 5.);
    if(modid == 0.) {
      return vec3(.5);
    } else if(modid == 1.) {
      return vec3(.1);
    } else if(modid == 2.) {
      return vec3(.5);
    } else if(modid == 3.) {
      return vec3(.1);
    } else if(modid == 4.) {
      return vec3(.1);
    }
    return vec3(.1);
  }
  
  Surface getSurface(int object_id, float rayDepth, vec3 sp) {
    return Surface(
      object_id, 
      rayDepth, 
      sp, 
      getObjectColour(object_id), 
      .5, 
      200.);
  }
  
  // The raymarch loop
  Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
    float sceneDist = 1e4;
    float rayDepth = start;
    int object_id = 0;
    for(int i = 0; i < maxIterations; i++) {
      sceneDist = world(ro + rd * rayDepth, object_id);
      
      if(sceneDist < stopThreshold || rayDepth > end) {
        break;
      }
      
      rayDepth += sceneDist * stepScale;
    }
    
    return getSurface(object_id, rayDepth, ro + rd * rayDepth);
  }
  
  // Calculated the normal of any given point in space. Intended to be cast from the point of a surface
  vec3 calculate_normal(in vec3 position) {
    vec3 grad = vec3(
      world(vec3(position.x + eps, position.y, position.z)) - world(vec3(position.x - eps, position.y, position.z)),
      world(vec3(position.x, position.y + eps, position.z)) - world(vec3(position.x, position.y - eps, position.z)),
      world(vec3(position.x, position.y, position.z + eps)) - world(vec3(position.x, position.y, position.z - eps))
    );
    
    return normalize(grad);
  }
  float calcSoftshadow( in vec3 ro, in vec3 rd, in float mint, in float tmax ) {
    float res = 1.0;
      float t = mint;
      float ph = 1e10; // big, such that y = 0 on the first iteration
      const int l = 32;

      for( int i=0; i<l; i++ ) {
        float h = world( ro + rd*t );
        float y = h*h/(2.0*ph);
        float d = sqrt(h*h-y*y);
        res = min( res, 10.0*d/max(0.0,t-y) );
        ph = h;

        t += h*.5;

        if( res<0.0001 || t>tmax ) break;

      }
      return clamp( res, 0.0, 1.0 );
  }
  
  vec3 lighting(Surface surface_object, vec3 cam) {
    
    // start with black
    vec3 sceneColour = vec3(0);
    
    // Surface normal
    vec3 normal = calculate_normal(surface_object.position);
    
    // Light position
    vec3 lp = vec3(-2, 2, -2);
    // Light direction
    vec3 ld = lp - surface_object.position;
    
    // light attenuation
    // For brightly lit scenes or global illumination (like sunlit), this can be limited to just normalizing the ld
    float len = length( ld );
    ld = normalize(ld);
    float lightAtten = min( 1.0 / ( 0.15*len ), 1.0 );
    lightAtten = 1.;
    
    // Scene values, mainly for fog
    float sceneLength = length(cam - surface_object.position);
    float sceneAttenuation = min( 1. / ( 0.015 * sceneLength * sceneLength ), 1. );
    
    // The surface's light reflection normal
    vec3 reflection_normal = reflect(-ld, normal);
    
    // Ambient Occlusion
    float ao = 1.;
    float shadows = calcSoftshadow( surface_object.position, ld, .001, 3. );
    // return vec3(shadows);
    
    // Object surface properties
    float diffuse = max(0., dot(normal, ld));
    float specular = max(0., dot( reflection_normal, normalize(cam - surface_object.position) ));
    
    // Bringing all of the lighting components together
    sceneColour += ( surface_object.colour * (diffuse + surface_object.ambient) + specular ) * light1_colour * lightAtten;
    // adding fog
    sceneColour = mix( sceneColour, fogColour, 1. - sceneAttenuation );
    
    return sceneColour * clamp(shadows+.4, 0., 1.);
  }
  
  vec3 path(float z) {
    return vec3(0,0,0.);
    return vec3(0,0,-100.+z);
  }

  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    // mouse rotation plane
    float s = sin(u_mouse.y*8.+3.6);
    float c = cos(u_mouse.y*8.+3.6);
    rotmouse = mat2(c, -s, s, c);
    // time rotation
    s = sin(u_time);
    c = cos(u_time);
    rot = mat2(c, -s, s, c);
    
    // movement
    movement = path(u_time);
    
    // Camera and look-at
    vec3 cam = vec3(1,1,-2)*.8;
    vec3 lookAt = vec3(0,0,0);
    
    // add movement
    lookAt += movement;
    cam += movement;
    
    // Unit vectors
    vec3 forward = normalize(lookAt - cam);
    vec3 right = normalize(vec3(forward.z, 0., -forward.x));
    vec3 up = normalize(cross(forward, right));
    
    // FOV
    float FOV = .4;
    
    // Ray origin and ray direction
    vec3 ro = cam;
    vec3 rd = normalize(forward + FOV * uv.x * right + FOV * uv.y * up);
    
    // Ray marching
    const float clipNear = 0.;
    const float clipFar = 32.;
    Surface objectSurface = rayMarch(ro, rd, clipNear, clipFar);
    float n = texture2D(u_noise, (uv.xy)*5.).r*.2;
    if(objectSurface.distance > clipFar) {
      gl_FragColor = vec4(vec3(n), 1.);
      return;
    }
    
    vec3 sceneColour = lighting(objectSurface, cam);
    // vec3 sceneColour = vec3(dist*.1);
    
    gl_FragColor = vec4(sceneColour+n, 1.);
  }
  
</script>
<!-- partial -->
  <script src='https://codepen.io/shubniggurath/pen/aPxLMx.js'></script><script  src="./script.js"></script>

</body>
</html>
