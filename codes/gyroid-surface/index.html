<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Gyroid Surface</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<script type="text/fragment" id="fragShader">
  precision highp float;

uniform vec2 u_resolution;
uniform float u_time;
uniform vec2 u_mouse;
uniform sampler2D s_noise;

uniform sampler2D b_noise;

varying vec2 v_uv;
  
  /* Raymarching constants */
  /* --------------------- */
  const float MAX_TRACE_DISTANCE = 5.;             // max trace distance
  const float INTERSECTION_PRECISION = 0.001;       // precision of the intersection
  const int NUM_OF_TRACE_STEPS = 256;               // max number of trace steps
  const float STEP_MULTIPLIER = .5;                 // the step mutliplier - ie, how much further to progress on each step
  
  /* Structures */
  /* ---------- */
  struct Camera {
    vec3 ro;
    vec3 rd;
    vec3 forward;
    vec3 right;
    vec3 up;
    float FOV;
  };
  struct Surface {
    float len;
    vec3 position;
    vec3 colour;
    float id;
    float steps;
    float AO;
  };
  struct Model {
    float dist;
    vec3 colour;
    float id;
  };
  
  vec2 getScreenSpace() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    return uv;
  }
  vec3 path(in float delta) {
    return vec3(cos(delta) * .2 + sin((u_time + delta) * .2) * .5, sin(delta * .5) * .4, delta);
  }
  
  //--------------------------------
  // Modelling
  //--------------------------------
float D( vec3 p ) { 
  return abs(dot(sin(p.yzx), cos(p.zxy)));
}
  float O(float f, float s) { return abs(f)-s; }
  Model model(vec3 p) {
    p.y -= .5;
    p.xy -= path(p.z).xy;
    float s = 40.;
    float a = (D(p*s+u_time*2.)-.5)/s;
    
    float d = D(p.yxz*4.)/4.;
    float d1=d;
    for(int i=0; i<2; i++) {
      d=O(d,.03);
    }
    d=length(vec2(d, a))-.05*smoothstep(-.2,1.4, d1);
    vec3 colour = vec3(1,0,0);
    return Model(d, colour, 1.);
  }
  Model map( vec3 p ){
    return model(p);
  }
  
  Surface calcIntersection( in Camera cam ){
    float h =  INTERSECTION_PRECISION*2.0;
    float rayDepth = 0.0;
    float hitDepth = -1.0;
    float id = -1.;
    float steps = 0.;
    float ao = 0.;
    vec3 position;
    vec3 colour;

    for( int i=0; i< NUM_OF_TRACE_STEPS ; i++ ) {
      if( abs(h) < INTERSECTION_PRECISION || rayDepth > MAX_TRACE_DISTANCE ) break;
      position = cam.ro+cam.rd*rayDepth;
      Model m = map( position );
      h = m.dist;
      rayDepth += h * STEP_MULTIPLIER;
      id = m.id;
      steps += 1.;
      ao += max(h, 0.);
      colour = m.colour;
    }

    if( rayDepth < MAX_TRACE_DISTANCE ) hitDepth = rayDepth;
    if( rayDepth >= MAX_TRACE_DISTANCE ) id = -1.0;

    return Surface( hitDepth, position, colour, id, steps, ao );
  }
  Camera getCamera(in vec2 uv, in vec3 pos, in vec3 target) {
    vec3 forward = normalize(target - pos);
    vec3 right = normalize(vec3(forward.z, 0., -forward.x));
    vec3 up = normalize(cross(forward, right));
    
    float FOV = .6;
    
    return Camera(
      pos,
      normalize(forward + FOV * uv.x * right + FOV * uv.y * up),
      forward,
      right,
      up,
      FOV
    );
  }
  
  
  float softshadow( in vec3 ro, in vec3 rd, in float mint, in float tmax, float w ) {
    float res = 1.0;
    float ph = 1e20;
    float t = mint;
    for( int i=0; i<256; i++ )
    {
        float h = map(ro + rd*t).dist;
        if( h<0.001 )
            return 0.0;
      if( t>=tmax ) break;
        float y = h*h/(2.0*ph);
        float d = sqrt(h*h-y*y);
        res = min( res, d/(w*max(0.0,t-y)) );
        ph = h;
        t += h*.5;
    }
    return res;
    
    
    // for( int i=0; i<16; i++ ) {
    //   float h = map( ro + rd*t ).dist;
    //   res = min( res, 8.0*h/t );
    //   t += clamp( h, 0.02, 0.10 );
    //   if( h<0.001 || t>tmax ) break;
    // }
    // return clamp( res, 0.0, 1.0 );
  }
  float calcAO( in vec3 pos, in vec3 nor ) {
    float occ = 0.0;
    float sca = 1.0;
    for( int i=0; i<5; i++ )
    {
      float hr = 0.01 + 0.12*float(i)/4.0;
      vec3 aopos =  nor * hr + pos;
      float dd = map( aopos ).dist;
      occ += -(dd-hr)*sca;
      sca *= 0.95;
    }
    return clamp( 1.0 - 3.0*occ, 0.0, 1.0 );    
  }
  vec3 shade(vec3 col, vec3 pos, vec3 nor, vec3 ref, Camera cam) {
    col=nor.bgr+1.;
    // lighitng        
    float occ = 1.;
    vec3 lp =vec3(0,.0,0);
    vec3  lig = normalize( lp-pos );
    float amb = clamp( 0.5+0.5*nor.y, 0.0, 1.0 );
    float dif = clamp( dot( nor, lig ), 0.0, 1.0 );
    float bac = clamp( dot( nor, normalize(vec3(-lig.x,0.0,-lig.z))), 0.0, 1.0 )*clamp( 1.0-pos.y,0.0,1.0);
    //float dom = smoothstep( -0.1, 0.1, ref.y );
    float fre = pow( clamp(1.0+dot(nor,cam.rd),0.0,1.0), 2.0 );
    // float spe = pow(clamp( dot( ref, lig ), 0.0, 1.0 ),16.0);

    float shad=clamp(softshadow( pos, lig, 0.01, 1.05, .02 )+.4, 0., 1.);
    dif *= shad;

    vec3 lin = vec3(0.0);
    lin += 2.20*dif*vec3(.95,0.80,0.60);
    lin += 0.40*amb*vec3(0.50,0.70,.80)*occ;
    lin += 0.30*bac*vec3(0.25,0.25,0.25)*occ;
    lin += 0.20*fre*vec3(1.00,1.00,1.00)*occ;
    col = col*lin;
    
    // col=vec3(shad);

    return col;
  }
  
  // Calculates the normal by taking a very small distance,
  // remapping the function, and getting normal for that
  vec3 calcNormal( in vec3 pos ){
    vec3 eps = vec3( 0.001, 0.0, 0.0 );
    vec3 nor = vec3(
      map(pos+eps.xyy).dist - map(pos-eps.xyy).dist,
      map(pos+eps.yxy).dist - map(pos-eps.yxy).dist,
      map(pos+eps.yyx).dist - map(pos-eps.yyx).dist );
    return normalize(nor);
  }
  
  vec3 render(Surface surface, Camera cam, vec2 uv) {
    vec3 surfaceNormal = calcNormal( surface.position );
    
    vec2 pp = uv;
    vec3 colour = vec3(.8);
    vec3 bg = clamp(colour, 0., 1.);

    if (surface.id == 1.){
      vec3 ref = reflect(cam.rd, surfaceNormal);
      colour = surfaceNormal;
      colour = clamp(shade(surface.colour, surface.position, surfaceNormal, ref, cam), 0., 1.);
      colour = mix(colour, bg, smoothstep(2., 10.,surface.len));
      
    }

    return colour;
  }
  
  void main() {
    vec2 uv = getScreenSpace();
    
    Camera cam = getCamera(uv, path(u_time), path(u_time+1.));
    
    Surface surface = calcIntersection(cam);
    
    gl_FragColor = vec4(render(surface, cam, uv), 1.);
  }
</script>
<!-- partial -->
  <script type="module" src="./script.js"></script>

</body>
</html>
