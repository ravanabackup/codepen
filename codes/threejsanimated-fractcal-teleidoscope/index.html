<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>ThreeJS - Animated Fractcal Teleidoscope</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<div id="settings">
  <div id="settingsToggle">âš™</div>
  <div id="settingsContent">
    <button id="fullscreen-toggle">Toggle Fullscreen</button>
    <div class="slider-container">
      <label for="powerSlider">Power:</label>
      <input type="range" id="powerSlider" min="1" max="16" step="0.1" value="5">
      <span class="value-display" id="powerValue">5.0</span>
    </div>
    <div class="slider-container">
      <label for="iterationsSlider">Iterations:</label>
      <input type="range" id="iterationsSlider" min="1" max="15" step="1" value="4">
      <span class="value-display" id="iterationsValue">4</span>
    </div>
    <h3>Animation Settings</h3>
    <div class="slider-container">
      <label for="rotationSpeedSlider">Rotation Speed:</label>
      <input type="range" id="rotationSpeedSlider" min="0" max="1" step="0.01" value="0.2">
      <span class="value-display" id="rotationSpeedValue">0.20</span>
    </div>
    <div class="slider-container">
      <label for="phaseShiftSpeedSlider">Phase Shift Speed:</label>
      <input type="range" id="phaseShiftSpeedSlider" min="0" max="0.5" step="0.01" value="0.1">
      <span class="value-display" id="phaseShiftSpeedValue">0.10</span>
    </div>
    <h3>Color Settings</h3>
    <div class="color-picker">
      <label for="sunColor">Sun Color:</label>
      <input type="color" id="sunColor" value="#FF0000">
    </div>
    <div class="color-picker">
      <label for="skyColor">Sky Color:</label>
      <input type="color" id="skyColor" value="#0000FF">
    </div>
    <div class="color-picker">
      <label for="fractalColor">Fractal Color:</label>
      <input type="color" id="fractalColor" value="#FFFFFF">
    </div>
    <h3>Teleidoscope Settings</h3>
    <div class="slider-container">
      <label for="teleidoscopeToggle">Enable Teleidoscope:</label>
      <input type="checkbox" id="teleidoscopeToggle">
    </div>
    <div class="slider-container">
      <label for="teleidoscopeSidesSlider">Teleidoscope Sides:</label>
      <input type="range" id="teleidoscopeSidesSlider" min="3" max="24" step="1" value="6">
      <span class="value-display" id="teleidoscopeSidesValue">6</span>
    </div>
    <div class="slider-container">
      <label for="teleidoscopeSpinSpeedSlider">Teleidoscope Spin Speed:</label>
      <input type="range" id="teleidoscopeSpinSpeedSlider" min="-1" max="1" step="0.01" value="0">
      <span class="value-display" id="teleidoscopeSpinSpeedValue">0.00</span>
    </div>
    <div class="slider-container">
      <label for="teleidoscopeZoomSlider">Teleidoscope Zoom:</label>
      <input type="range" id="teleidoscopeZoomSlider" min="0.1" max="5" step="0.1" value="1">
      <span class="value-display" id="teleidoscopeZoomValue">1.00</span>
    </div>
    <h3>Randomize Settings</h3>
    <div class="slider-container">
      <label for="randomizeToggle">Enable Randomize:</label>
      <input type="checkbox" id="randomizeToggle">
    </div>
    <div class="slider-container">
      <label for="randomizeDurationSlider">Randomize Duration (s):</label>
      <input type="range" id="randomizeDurationSlider" min="5" max="60" step="1" value="30">
      <span class="value-display" id="randomizeDurationValue">30</span>
    </div>
    <div class="slider-container">
      <label for="randomizeTransitionSlider">Randomize Transition (s):</label>
      <input type="range" id="randomizeTransitionSlider" min="0" max="15" step="0.1" value="3">
      <span class="value-display" id="randomizeTransitionValue">3.0</span>
    </div>
    <div id="countdownDisplay">Next change in: <span id="countdownValue">30</span>s</div>
    <h3>Current Transitioning Values</h3>
    <div id="transitioningValues">
      <div>Power: <span id="currentPower"></span></div>
      <div>Rotation Speed: <span id="currentRotationSpeed"></span></div>
      <div>Phase Shift Speed: <span id="currentPhaseShiftSpeed"></span></div>
      <div>Iterations: <span id="currentIterations"></span></div>
      <div>Sun Color: <span id="currentSunColor"></span></div>
      <div>Sky Color: <span id="currentSkyColor"></span></div>
      <div>Fractal Color: <span id="currentFractalColor"></span></div>
      <div>Teleidoscope Sides: <span id="currentTeleidoscopeSides"></span></div>
      <div>Teleidoscope Spin Speed: <span id="currentTeleidoscopeSpinSpeed"></span></div>
      <div>Teleidoscope Zoom: <span id="currentTeleidoscopeZoom"></span></div>
    </div>
  </div>
</div>

<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r128/three.min.js"></script>

<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
    gl_Position = vec4(position, 1.0);
}
</script>

<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec3 iResolution;
uniform float iTime;
uniform float power;
uniform float rotationSpeed;
uniform float phaseShiftSpeed;
uniform int iterations;
uniform vec3 sunColor;
uniform vec3 skyColor;
uniform vec3 fractalColor;
uniform bool teleidoscopeEnabled;
uniform float teleidoscopeSides;
uniform float teleidoscopeSpinSpeed;
uniform float teleidoscopeZoom;
uniform bool randomizeEnabled;
uniform float randomizeDuration;
uniform float randomizeTransition;
uniform float randomizeProgress;
uniform float targetPower;
uniform float targetRotationSpeed;
uniform float targetPhaseShiftSpeed;
uniform int targetIterations;
uniform vec3 targetSunColor;
uniform vec3 targetSkyColor;
uniform vec3 targetFractalColor;
uniform float targetTeleidoscopeSides;
uniform float targetTeleidoscopeSpinSpeed;
uniform float targetTeleidoscopeZoom;

#define phase_shift_on 

float stime, ctime;
void ry(inout vec3 p, float a) {  
    float c,s;vec3 q=p;  
    c = cos(a); s = sin(a);  
    p.x = c * q.x + s * q.z;  
    p.z = -s * q.x + c * q.z; 
}  

float pixel_size = 0.0;

vec2 teleidoscope(vec2 uv) {
    if (!teleidoscopeEnabled) return uv;
    
    float angle = atan(uv.y, uv.x);
    float radius = length(uv);
    
    float segmentAngle = 2.0 * 3.14159265359 / teleidoscopeSides;
    angle = mod(angle + iTime * teleidoscopeSpinSpeed, segmentAngle);
    if (angle > segmentAngle / 2.0) {
        angle = segmentAngle - angle;
    }
    
    radius *= teleidoscopeZoom;
    
    return vec2(cos(angle), sin(angle)) * radius;
}

vec3 mb(vec3 p) {
    p.xyz = p.xzy;
    vec3 z = p;
    vec3 dz=vec3(0.0);
    float r, theta, phi;
    float dr = 1.0;
    
    float t0 = 1.0;
    for(int i = 0; i < 15; ++i) {
        if (i >= iterations) break;
        r = length(z);
        if(r > 2.0) continue;
        theta = atan(z.y, z.x);
        #ifdef phase_shift_on
        phi = asin(z.z / r) + iTime * phaseShiftSpeed;
        #else
        phi = asin(z.z / r);
        #endif
        
        dr = pow(r, power - 1.0) * dr * power + 1.0;
    
        r = pow(r, power);
        theta = theta * power;
        phi = phi * power;
        
        z = r * vec3(cos(theta)*cos(phi), sin(theta)*cos(phi), sin(phi)) + p;
        
        t0 = min(t0, r);
    }
    return vec3(0.5 * log(r) * r / dr, t0, 0.0);
}

vec3 f(vec3 p) { 
    ry(p, iTime * rotationSpeed);
    return mb(p); 
}

float softshadow(vec3 ro, vec3 rd, float k ) { 
    float akuma=1.0,h=0.0; 
    float t = 0.01;
    for(int i=0; i < 50; ++i) { 
        h=f(ro+rd*t).x; 
        if(h<0.001)return 0.02; 
        akuma=min(akuma, k*h/t); 
        t+=clamp(h,0.01,2.0); 
    } 
    return akuma; 
} 

vec3 nor(in vec3 pos) {
    vec3 eps = vec3(0.001,0.0,0.0);
    return normalize(vec3(
        f(pos+eps.xyy).x - f(pos-eps.xyy).x,
        f(pos+eps.yxy).x - f(pos-eps.yxy).x,
        f(pos+eps.yyx).x - f(pos-eps.yyx).x));
}

vec3 intersect(in vec3 ro, in vec3 rd) {
    float t = 1.0;
    float res_t = 0.0;
    float res_d = 1000.0;
    vec3 c, res_c;
    float max_error = 1000.0;
    float d = 1.0;
    float pd = 100.0;
    float os = 0.0;
    float step = 0.0;
    float error = 1000.0;
    
    for(int i=0; i<48; i++) {
        if(error < pixel_size*0.5 || t > 20.0) {
            break;
        }
        
        c = f(ro + rd*t);
        d = c.x;

        if(d > os) {
            os = 0.4 * d*d/pd;
            step = d + os;
            pd = d;
        }
        else {
            step =-os; os = 0.0; pd = 100.0; d = 1.0;
        }

        error = d / t;

        if(error < max_error) {
            max_error = error;
            res_t = t;
            res_c = c;
        }
    
        t += step;
    }
    if(t>20.0) res_t=-1.0;
    return vec3(res_t, res_c.y, res_c.z);
}

void mainImage(out vec4 fragColor, in vec2 fragCoord) { 
    vec2 q=fragCoord.xy/iResolution.xy; 
    vec2 uv = -1.0 + 2.0*q; 
    uv.x*=iResolution.x/iResolution.y; 
    
    uv = teleidoscope(uv);
    
    pixel_size = 1.0/(iResolution.x * 3.0);
    stime=0.7+0.3*sin(iTime*0.4); 
    ctime=0.7+0.3*cos(iTime*0.4); 

    vec3 ta=vec3(0.0,0.0,0.0); 
    vec3 ro = vec3(0.0, 3.*stime*ctime, 3.*(1.-stime*ctime));

    vec3 cf = normalize(ta-ro); 
    vec3 cs = normalize(cross(cf,vec3(0.0,1.0,0.0))); 
    vec3 cu = normalize(cross(cs,cf)); 
    vec3 rd = normalize(uv.x*cs + uv.y*cu + 3.0*cf);  

    vec3 sundir = normalize(vec3(0.1, 0.8, 0.6)); 
    vec3 sun = sunColor; 
    vec3 skycolor = skyColor; 

    vec3 bg = exp(uv.y-2.0)*skyColor;

    float halo=clamp(dot(normalize(vec3(-ro.x, -ro.y, -ro.z)), rd), 0.0, 1.0); 
    vec3 col=bg+vec3(1.0,0.8,0.4)*pow(halo,17.0); 

    float t=0.0;
    vec3 p=ro; 
     
    vec3 res = intersect(ro, rd);
    if(res.x > 0.0) {
        p = ro + res.x * rd;
        vec3 n=nor(p); 
        float shadow = softshadow(p, sundir, 10.0);

        float dif = max(0.0, dot(n, sundir)); 
        float sky = 0.6 + 0.4 * max(0.0, dot(n, vec3(0.0, 1.0, 0.0))); 
        float bac = max(0.3 + 0.7 * dot(vec3(-sundir.x, -1.0, -sundir.z), n), 0.0); 
        float spe = max(0.0, pow(clamp(dot(sundir, reflect(rd, n)), 0.0, 1.0), 10.0)); 

        vec3 lin = 4.5 * sun * dif * shadow; 
        lin += 0.8 * bac * sun; 
        lin += 0.6 * sky * skycolor*shadow; 
        lin += 3.0 * spe * shadow; 

        res.y = pow(clamp(res.y, 0.0, 1.0), 0.55);
        vec3 tc0 = 0.5 + 0.5 * sin(3.0 + 4.2 * res.y + vec3(0.0, 0.5, 1.0));
        col = lin * fractalColor *  0.2 * tc0;
        col=mix(col,bg, 1.0-exp(-0.001*res.x*res.x)); 
    } 

    col=pow(clamp(col,0.0,1.0),vec3(0.45)); 
    col=col*0.6+0.4*col*col*(3.0-2.0*col);  
    col=mix(col, vec3(dot(col, vec3(0.33))), -0.5);  
    col*=0.5+0.5*pow(16.0*q.x*q.y*(1.0-q.x)*(1.0-q.y),0.7);  
    fragColor = vec4(col.xyz, smoothstep(0.55, .76, 1.-res.x/5.)); 
}

void main() {
    mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<!-- partial -->
  <script  src="./script.js"></script>

</body>
</html>
