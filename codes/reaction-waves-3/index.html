<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Reaction Waves 3</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/88/three.min.js"></script>
<script id="vertexShader" type="x-shader/x-vertex">
    void main() {
        gl_Position = vec4( position, 1.0 );
    }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 u_resolution;
  uniform vec4 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  uniform sampler2D u_buffer;
  uniform sampler2D u_buffer2;
  uniform bool u_renderpass;
  uniform bool u_renderpass2;
  uniform int u_frame;
  uniform bool u_mousemoved;
  
  #define PI 3.141592653589793
  #define TAU 6.283185307179586

  const bool addNoise = false; // Whether to add noise to the rays
  const float decay = .99; // the amount to decay each sample by
  const float exposure = .9; // the screen exposure
  const float lightStrength = 3.5;
  const vec3 lightcolour = vec3(1.2, 1.15, 1.1)*2.; // the colour of the light
  const vec3 falloffcolour = vec3(.3); // the colour of the falloff
  const vec3 bgcolour = vec3(.1, .15, .15); // the base colour of the render
  const float falloff = .3;
  const int samples = 8; // The number of samples to take
  const float density = .98; // The density of the "smoke"
  const float weight = .25; // how heavily to apply each step of the supersample
  const int octaves = 1; // the number of octaves to generate in the FBM noise
  const float seed = 43758.5453123; // A random seed :)
  
  float random2d(vec2 uv) {
    uv /= 256.;
    vec4 tex = texture2D(u_noise, uv);
    return mix(tex.x, tex.y, tex.a);
  }
  vec2 random2(vec2 st, float seed){
      st = vec2( dot(st,vec2(127.1,311.7)),
                dot(st,vec2(269.5,183.3)) );
      return -1.0 + 2.0*fract(sin(st)*seed);
  }
  
  // Value Noise by Inigo Quilez - iq/2013
  // https://www.shadertoy.com/view/lsf3WH
  float noise(vec2 st, float seed) {
    vec3 x = vec3(st, 1.);
    vec3 p = floor(x);
    vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);
    vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
    vec2 rg = texture2D(u_noise, (uv+0.5) / 256., 0.).yx - .5;
    return mix( rg.x, rg.y, f.z );
  }
  
  float fbm1(in vec2 _st, float seed) {
    float v = 0.0;
    float a = 0.5;
    vec2 shift = vec2(100.0);
    // Rotate to reduce axial bias
    mat2 rot = mat2(cos(0.5), sin(0.5),
                    -sin(0.5), cos(0.50));
    for (int i = 0; i < octaves; ++i) {
        v += a * noise(_st, seed);
        _st = rot * _st * 2.0 + shift;
        a *= 0.4;
    }
    return v + .4;
  }
  
  float pattern(vec2 uv, float seed, float time, inout vec2 q, inout vec2 r) {

    q = vec2( fbm1( uv + vec2(0.0,0.0), seed ),
                   fbm1( uv + vec2(5.2,1.3), seed ) );

    r = vec2( fbm1( uv + 4.0*q + vec2(1.7 - time / 2.,9.2), seed ),
                   fbm1( uv + 4.0*q + vec2(8.3 - time / 2.,2.8), seed ) );

    float rtn = fbm1( uv + 4.0*r, seed );

    return rtn;
  }

  float shapes(vec2 uv) {
    
    float tex = texture2D(u_buffer2, uv*.1).y;
    float shade1 = 1. - smoothstep(.0, 0.8, tex);
    // shade1 = mix(shade1, 0., smoothstep(.8, 0.2, tex)*.1);
    // shade1 -= smoothstep(.5, 0., texture2D(u_buffer2, uv*.2).y);
    
    return clamp(shade1, 0., 1.);
    
  }
  
  float occlusion(vec2 uv, vec2 lightpos, float objects) {
    
    return (1. - smoothstep(0.0, lightStrength, length(lightpos - uv))) * (1. - objects);
  }
  
  vec4 mainRender(vec2 uv, inout vec4 fragcolour) {
  
    float scale = 6.;
    uv *= scale;
    
    float exposure = exposure + (sin(u_time) * .5 + 1.) * .05;

    vec2 _uv = uv;
    vec2 lightpos = (vec2(u_mouse.x, u_mouse.y * -1.)) / u_resolution.y;
    lightpos = u_mouse.xy * scale;
    
    if(!u_mousemoved) {
      lightpos.x += cos(u_time * .25)*3.;
      lightpos.y += sin(u_time * .5)*2.;
    }
    
    float obj = shapes(uv);
    float map = occlusion(uv, lightpos, obj);

    float _pattern = 0.;
    vec2 q = vec2(0.);
    vec2 r = vec2(0.);
    if(addNoise) {
      _pattern = pattern(_uv * 3. , seed, u_time, q, r) / 2.;
    }

    vec2 dtc = (_uv - lightpos) * (1. / float(samples) * density);
    float illumination_decay = 1.;
    vec3 basecolour = bgcolour - obj * .02;

    for(int i=0; i<samples; i++) {
      _uv -= dtc;
      if(addNoise) {
        uv += _pattern / 16.;
      }
      
      float movement = u_time * 20. * float(i + 1);
      
      float dither = random2d(uv * 512. + mod(vec2(movement*sin(u_time * .5), -movement), 1000.)) * 2.;

      float stepped_map = occlusion(uv, lightpos, shapes(_uv+dtc*dither));
      stepped_map *= illumination_decay * weight;
      illumination_decay *= decay;

      map += stepped_map;
    }

    float l = length(lightpos - uv);

    vec3 lightcolour = mix(lightcolour, falloffcolour, l*falloff);

    vec3 colour = vec3(basecolour+map*exposure*lightcolour);
    // colour *= vec3(sin(u_time+uv.y)*0.2, cos(u_time+uv.x)*0.2, cos(u_time*.3+uv.x-uv.y)*0.3)*1.5+.5;
    
    fragcolour = vec4(colour,1.0);
    
    return fragcolour;
  }

  vec2 hash2(vec2 p)
  {
    vec2 o = texture2D( u_noise, (p+0.5)/256.0, -100.0 ).xy;
    return o;
  }
  
  vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix( vec3(1.0), rgb, c.y);
  }
  
  vec3 domain(vec2 z){
    return vec3(hsb2rgb(vec3(atan(z.y,z.x)/TAU,1.,1.)));
  }
  vec3 colour(vec2 z) {
      return domain(z);
  }

  // Shorthand, so that the texture lines read a little better.
  // Borrowed from Shane
  vec4 tx(vec2 p, sampler2D buffer){ return texture2D(buffer, p); }
  
  // 25 (or 9) tap Laplacian -- Gaussian Laplacian, to be more precise. I think of it as taking
  // the sum of the partial second derivatives of a blurry 2D height map... in each channel...
  // I think I'm making things more confusing, but it works anyway. :D Seriously, just look
  // up the Laplacian operator of a 2D function.
  // Borrowed from Shane
  vec4 Laplacian(vec2 p, sampler2D buffer) {

    // Kernel matrix dimension, and a half dimension calculation.
    const int mDim = 5, halfDim = (mDim - 1)/2;

//     float scale = .25;
//     float kernel[25];
//     kernel[0] = 1. * scale;
//     kernel[1] = 1. * scale;
//     kernel[2] = 1. * scale;
//     kernel[3] = 1. * scale;
//     kernel[4] = 1. * scale;

//     kernel[5] = 1. * scale;
//     kernel[6] = 1. * scale;
//     kernel[7] = 1. * scale;
//     kernel[8] = 1. * scale;
//     kernel[9] = 1. * scale;

//     kernel[10] = 1. * scale;
//     kernel[11] = 1. * scale;
//     kernel[12] = -24. * scale;
//     kernel[13] = 1. * scale;
//     kernel[14] = 1. * scale;

//     kernel[15] = 1. * scale;
//     kernel[16] = 1. * scale;
//     kernel[17] = 1. * scale;
//     kernel[18] = 1. * scale;
//     kernel[19] = 1. * scale;

//     kernel[20] = 1. * scale;
//     kernel[21] = 1. * scale;
//     kernel[22] = 1. * scale;
//     kernel[23] = 1. * scale;
//     kernel[24] = 1. * scale;
    
    
    
    float kernel[25];
    kernel[0] = 0.;
    kernel[1] = 0.;
    kernel[2] = 0.25;
    kernel[3] = 0.;
    kernel[4] = 0.;

    kernel[5] = 0.;
    kernel[6] = 0.25;
    kernel[7] = 0.50;
    kernel[8] = 0.25;
    kernel[9] = 0.;

    kernel[10] = 0.25;
    kernel[11] = 0.50;
    kernel[12] = -4.0;
    kernel[13] = 0.50;
    kernel[14] = 0.25;

    kernel[15] = 0.;
    kernel[16] = 0.25;
    kernel[17] = 0.50;
    kernel[18] = 0.25;
    kernel[19] = 0.;

    kernel[20] = 0.;
    kernel[21] = 0.;
    kernel[22] = 0.25;
    kernel[23] = 0.;
    kernel[24] = 0.;
    
    vec4 col = vec4(0);

    float px = 1./u_resolution.y; 

    for (int j=0; j<mDim; j++){
      for (int i=0; i<mDim; i++){ 
        col += kernel[j*mDim + i]*tx(p + vec2(i - halfDim, j - halfDim)*px, buffer);
      }
    }

    return col;
  }


  void render( out vec4 fragColor, in vec2 fragCoord, sampler2D thebuffer, int step ) {

    vec2 p = fragCoord.xy/u_resolution.xy;

    vec4 rdVal = texture2D(thebuffer, p);

    vec2 lap = Laplacian(p, thebuffer).xy;

    float mixamt = clamp(length(p-.5) * 2., 0., 1.);

    float feed = 0.04567;
    float kill = 0.06649;
    vec2 dAB = vec2(.19685, .05405);
    
    if(step == 1) {
      rdVal = texture2D(thebuffer, p * 1.00002 + 0.00005);
      feed = 0.0393167;
      kill = 0.059149;

      dAB = vec2(.22685, .08405)*.9;
    }
    
    float shade_raw = texture2D(u_buffer, (p+.5+vec2(u_time*.05, sin(u_time*.1)*.1))).y;
    float shade1 = smoothstep(0.3, 0.2, shade_raw);
    float shade2 = smoothstep(0.4, 0.0, shade_raw);
    
    if(step == 2) {
      // shade1 = 1.;
      feed = mix(0.040867, .049, shade1);
      kill = mix(0.0819149, .0576, shade1);
      dAB = mix(vec2(.06685, .03405)*.5, dAB, shade1);
    }

    const float t = 1.5; 

    vec2 diffusion = dAB*lap;

    vec2 reaction = vec2(rdVal.x*rdVal.y*rdVal.y)*vec2(-1, 1);

    vec2 feedKill = vec2(feed*(1. - rdVal.x), (kill + feed)*rdVal.y)*vec2(1, -1);
    vec2 delta = diffusion + reaction + feedKill;

    fragColor.xy = clamp(rdVal.xy + delta*t, 0., 1.);

    fragColor.zw = u_resolution.xy;
    if(step == 1) {
      if( u_frame<10 || u_mouse.z == 1. ) {
        vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
        vec2 q, r;
        float n = pattern(uv*10., 12345., u_time, q, r);
        n *= n*n*n*n*n*n*n*40.;
        
        fragColor.y = mix(fragColor.y, 1., n);
        fragColor.x = mix(fragColor.x, 0., n);
      }
    } else {
      if( u_frame<10) {
        vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
        vec2 q, r;
        float n = pattern(uv*10., 12345., u_time, q, r);
        n *= n*n*n*n*n*n*n*40.;
        
        fragColor.y = mix(fragColor.y, 1., n);
        fragColor.x = mix(fragColor.x, 0., n);
      }
    }

  }
  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    vec2 mouse = u_mouse.xy - uv;
    
    float shade = smoothstep(.1, .15, length(mouse));
    
    vec4 fragcolour = vec4(shade);
    
    if(u_mouse.z == 1.) {
      fragcolour = vec4(shade, 0., 0., 0.);
    } else if(u_mouse.a == 1.) {
      fragcolour = vec4(0., shade, 0., 0.);
    }
    
    // vec3 fragcolour = colour(uv);
    
    if(u_renderpass == true) {
      render( gl_FragColor, gl_FragCoord.xy, u_buffer, 1 );
    } else if(u_renderpass2 == true) {
      render( gl_FragColor, gl_FragCoord.xy, u_buffer2, 2 );
    } else {
  
      mainRender(uv, gl_FragColor);
    }

    // gl_FragColor = fragcolour;
  }
</script>


<div id="container" touch-action="none"></div>
<!-- partial -->
  <script src='https://s3-us-west-2.amazonaws.com/s.cdpn.io/982762/ccapture.js'></script><script  src="./script.js"></script>

</body>
</html>
