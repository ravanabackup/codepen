<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - #curiouslyminded with Sean Zellmer</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<!-- Shader Session with Sean Zellmer | 2021
 *
 * Sean: 
 * https://www.instagram.com/lejeunerenard/
 * https://twitter.com/lejeunerenard/
 *
 * We're curiouslyminded (eliza & ilithya):
 * https://www.curiouslyminded.xyz
 * https://www.twitch.tv/curiouslyminded
 * https://www.youtube.com/curiouslyminded -->

<div id="shadercollab"></div>
<script id="vertex" type="x-shader/x-vertex">
	void main() { gl_Position = vec4(position, 1.0); }
</script>

<script id="fragment" type="x-shader/x-fragment">
precision highp float;

uniform vec2 u_resolution;
uniform float u_time;
	
// Pallete function by Inigo Quilez - iquilezles.org
// a - brigthness, b - contrast, c - osc, d - phase
vec3 palette(in vec3 t, in vec3 a, in vec3 b, in vec3 c, in vec3 d) { return a + b*cos( 6.28318*(c*t+d) ); }
	
// Got rotation functions from akella - twitter.com/akella
mat4 rotationMatrix(vec3 axis, float angle) {
    axis = normalize(axis);
    float s = sin(angle);
    float c = cos(angle);
    float oc = 1.0 - c;
    
    return mat4(oc * axis.x * axis.x + c, 
				oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           
				oc * axis.y * axis.z - axis.x * s,  0.0,
                oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           
				0.0,
                0.0,                                
				0.0,                                
				0.0,                                
				1.0);
}

vec3 rotate(vec3 v, vec3 axis, float angle) {
	mat4 m = rotationMatrix(axis, angle);
	return (m * vec4(v, 1.0)).xyz;
}

// Torus function by Inigo Quilez - iquilezles.org
// p - position, t - size
float sdTorus(vec3 p, vec2 t) {
	vec2 q = vec2(length(p.xz)-t.x,p.y);
	return length(q)-t.y;
}
	
float scene(vec3 pos) {
	vec3 rotation = rotate(vec3(pos.x, pos.y, pos.z+1.5), vec3(1.), u_time*2.);	
	
	vec3 wPos = rotation; // warp position
	const float warpScale = 1.;
	
	vec2 size = vec2(2.0);
	
	// wPos.xy = mod(wPos.xy + size*0.5, size) - size*0.5; // multiple shapes
	
	// Cosine domain warp
	wPos += warpScale * 0.10000 * cos( 3. * wPos.yzx + u_time);
	wPos += warpScale * 0.05000 * cos(11. * wPos.yzx + u_time);
	wPos += warpScale * 0.02500 * cos(17. * wPos.yzx + u_time);
	
	rotation = wPos;
	
	rotation = rotation.xzy;
	
	float r = 0.5;	
	float torus = sdTorus(rotation, vec2(r, r*0.6));
	
	return 0.4 * torus;
}

const int RAYMARCH_MAX_STEPS = 200;
const float RAYMARCH_MAX_DIST = 10.;
const float EPSILON = 0.0001;

// hoisting for raymarch fn return val	
vec4 shade (vec3 pos, vec3 rayDir, float rayDepth);
	
vec4 raymarch(vec3 rayDir, vec3 pos) {
	// Define the start state
	// reset to 0 steps
	float currentDist = 0.0; 
	float rayDepth = 0.0;
	vec3 rayLength = vec3(0.0);
	
	// shooting the ray
 	for (int i=0; i < RAYMARCH_MAX_STEPS; i++) {
     	// steps traveled
		currentDist = scene(pos + rayDir * rayDepth);
		rayDepth += currentDist;
		
 	    if (currentDist < EPSILON) return shade(pos + rayDir * rayDepth, rayDir, rayDepth); // We're inside the scene - magic happens here
 		if (rayDepth > RAYMARCH_MAX_DIST) return vec4(0, 0, 0, 1.); // We've gone too far
	}
	
	return vec4(0, 0, 0, 1.);
}
	
vec3 getNormal (in vec3 pos, in float depth) {
	const vec2 epsilon = vec2(0.0001, 0.);
	vec3 nor = vec3(
		scene(pos + epsilon.rgg) - scene(pos - epsilon.rgg), // x
		scene(pos + epsilon.grg) - scene(pos - epsilon.grg), // y
		scene(pos + epsilon.ggr) - scene(pos - epsilon.ggr) // z
	);
	return normalize(nor);
}
	
float diffuse (in vec3 light, in vec3 nor) {
	return clamp(0., 1., dot(nor, light));
}
	
vec3 baseColor (in vec3 pos, in vec3 nor, in vec3 rayDir, in float rayDepth) {
	vec3 color = vec3(0);
	
	float dNR = dot(nor, -rayDir);
	
	color = palette(vec3(dNR), vec3(0.5), vec3(0.5), vec3(1), vec3(0, 0.33, 0.67));
	return color;
}
	
vec4 shade (vec3 pos, vec3 rayDir, float rayDepth) {
	vec3 nor = getNormal(pos, rayDepth);
	
	nor += 0.1 * sin(13. * nor + u_time);
	nor = normalize(nor);
	
	vec3 color = palette(
		nor,
		vec3(0.65, 0.95, 0.85), // brightness
		vec3(rayDepth) * 2., // contrast
		vec3(1), // osc
		vec3(0., 0.33, 0.67) // phase
	);
	
	vec3 lightPos = rotate(vec3(1.), vec3(0, 1, 0), 3.14 * u_time);
	
	float dif = diffuse(lightPos, nor);
	color = dif * baseColor(pos, nor, rayDir, rayDepth);
	vec4 shapeColor = vec4(color, 1.0);

	return shapeColor;
}
	
void main() {
	vec2 uv = (gl_FragCoord.xy - u_resolution * .5) / u_resolution.yy; 
	
	vec3 camPos = vec3(0.0, 0.0, 5.0); // x, y, z axis	

	vec3 rayDir = normalize(vec3(uv, -1.0)); // DOF
	
    gl_FragColor = vec4(raymarch(rayDir, camPos));
}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/r128/three.min.js'></script><script  src="./script.js"></script>

</body>
</html>
