<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Codepen Challenge: Infinite Maze</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="webgl" width="500" height="1758"></canvas>

<script id="vertexShader" type="x-shader/x-vertex">
  attribute vec4 a_position;
  
  uniform mat4 u_modelViewMatrix;
  uniform mat4 u_projectionMatrix;
  
  void main() {
    gl_Position = a_position;
  }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
  precision highp float;
  
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;

  vec2 hash22(vec2 uv) { 
    return texture2D(u_noise, uv/255.).xy;
  }
  
  struct Tile {
    vec2 uv;
    vec2 id;
    vec2 hash;
  };
  
  Tile getTile(vec2 uv) {
    vec2 subuv = fract(uv);
    vec2 tile = floor(uv);
    
    return Tile(subuv, tile, hash22(tile));
  }
  
  vec2 getTileField(vec2 uv, float hash) {
    const vec3 offsets = vec3(1,1,0);
    const float fields = 7.;
    const float fieldinc = 1. / fields;
    
    if(hash < fieldinc) {
      // right to bottom, top to left
      return vec2(length(uv), length(uv - offsets.xy));
    } else if(hash < fieldinc * 2.) {
      // right to top, bottom to left
      return vec2(length(uv - offsets.zy), length(uv - offsets.xz));
    } else if(hash < fieldinc * 3.) {
      // top to bottom, left to right
      return vec2(abs(uv.x), abs(uv.y));
    } else if(hash < fieldinc * 4.) {
      // dots top and bottom, bar left to right
      return vec2(
        min(
          length(uv - offsets.xz * .5) + .5,
          length(uv - offsets.xz * .5 - offsets.zy) + .5
          ), abs(uv.y));
    } else if(hash < fieldinc * 5.) {
      // dots left and right, bar top to bottom
      return vec2(
        abs(uv.x),
        min(
          length(uv - offsets.zy * .5) + .5,
          length(uv - offsets.zy * .5 - offsets.xz) + .5
          ));
    } else if(hash < fieldinc * 6.) {
      // dots top and right, curve left to bottom
      return vec2(
        length(uv),
        min(
          length(uv - offsets.xz * .5 - offsets.zy) + .5,
          length(uv - offsets.zy * .5 - offsets.xz) + .5
          ));
    } else if(hash < fieldinc * 7.) {
      // dots left and bottom, curve top to right
      return vec2(
        length(uv - offsets.xy),
        min(
          length(uv - offsets.xz * .5) + .5,
          length(uv - offsets.zy * .5) + .5
          ));
    }
    
  }
  
  vec3 getTruchet(vec2 uv) {
    Tile tile = getTile(uv);
    vec2 field = getTileField(tile.uv, tile.hash.x);
    vec2 field2 = getTileField(tile.uv, tile.hash.y);
    
    float strokehalf = .17;
    const float shadowsize = .05;
    const float aa = .01;
    
    float t1 = 0.;
    float t1shadow = 0.;
    
    if(tile.hash.y < .5) {
      tile = getTile(uv * 2.);
      field = getTileField(tile.uv, tile.hash.x);
      field2 = getTileField(tile.uv, tile.hash.y);
      
      if(tile.hash.y < .3) {
        tile = getTile(uv * 4.);
        field = getTileField(tile.uv, tile.hash.x);
        field2 = getTileField(tile.uv, tile.hash.y);
        
        t1 = clamp(smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.x) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.x) + smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.y) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.y), 0., 1.);
        t1shadow = smoothstep(.5 - strokehalf - shadowsize, .5 - strokehalf - shadowsize + aa, field.x) * smoothstep(.5 + strokehalf + shadowsize, .5 + strokehalf + shadowsize - aa, field.x);
      } else {
        t1 = 1.-clamp(smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.x) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.x) + smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.y) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.y), 0., 1.);
        t1shadow = smoothstep(.5 - strokehalf - shadowsize, .5 - strokehalf - shadowsize + aa, field.x) * smoothstep(.5 + strokehalf + shadowsize, .5 + strokehalf + shadowsize - aa, field.x);
      }
      
    } else {
      t1 = clamp(smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.x) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.x) + smoothstep(.5 - strokehalf, .5 - strokehalf + aa, field.y) * smoothstep(.5 + strokehalf, .5 + strokehalf - aa, field.y), 0., 1.);
      t1shadow = smoothstep(.5 - strokehalf - shadowsize, .5 - strokehalf - shadowsize + aa, field.x) * smoothstep(.5 + strokehalf + shadowsize, .5 + strokehalf + shadowsize - aa, field.x);
    }
    
    vec3 colour = vec3(0.);
    
    colour = vec3(t1);
    
    // colour = mix(colour, vec3(.5), clamp(step(tile.uv.x, .02) + step(tile.uv.y, .02), 0., 1.));
    
    return colour;
  }
  
  vec2 getScreenSpace() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    float t = u_time;
    
    float c = cos(sin(t) * .5) * 2.5;
    float s = sin(sin(t) * .5) * 2.5;
    uv *= mat2(c, -s, s, c);
    
    uv *= 3.;
    
    uv += vec2(u_time * 2., sin(u_time) * 2.) * 3.;
    
    return uv;
  }

  void main() {
    vec2 uv = getScreenSpace();
    
    vec3 colour = getTruchet(uv);

    gl_FragColor = vec4(colour,1.0);
  }
  
</script>
<!-- partial -->
  <script src='https://codepen.io/shubniggurath/pen/aPxLMx.js'></script><script  src="./script.js"></script>

</body>
</html>
