<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>WebGL | Multi-scale Truchet Shader</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<!-- VertexShader code here -->
<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
precision highp float;
  in vec4 position;

  void main() {
    gl_Position = vec4( position );
  }
</script>

<!-- FragmentShader code here -->
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
precision highp float;
out vec4 fragColor;
uniform vec2 u_resolution;
uniform float u_time;

#define R           u_resolution
#define T           u_time
#define PI          3.14159265358
#define PI2         6.28318530718

  
// @Shane 2/2 hash.
vec2 hash22(vec2 p) { float n = sin(dot(p,vec2(57,27)));return fract(vec2(274134,32768)*n);}
mat2 rot (float a) { return mat2(cos(a),sin(a),-sin(a),cos(a)); }

//@iq hsv2rgb palettes & box
vec3 hsv(vec3 c) {
    vec3 rgb = clamp( abs(mod(c.x*6.+vec3(0,4,2),6.)-3.)-1.,0.,1.);
    return c.z * mix( vec3(1),rgb,c.y);
}

float box( vec2 p, vec2 b){ vec2 d = abs(p)-b; return length(max(d,0.)) + min(max(d.x,d.y),0.);}

void main() {
    vec2 F = gl_FragCoord.xy;
    vec2 uv = (2.*F.xy-R.xy)/max(R.x,R.y);
    vec2 suv= uv;

    // upscale
    uv*=3.;
    
    // turn and move
    uv *= rot(-T*.08);
    uv.x -= T*.35;

    // threshold values
    const vec2 rndX[4]=vec2[4]( vec2(.35, .55), vec2(.5, .25), vec2(.5, .5), vec2(.5, .7));
    // distance field values.
    vec4 d=vec4(1e5), s=vec4(1e5), b=vec4(1e5), e=vec4(1e5);
    // start level
    float level=1., grid = 1e5;
         
    // Main loop and neighbor checking from @Shane's shader.
    // https://www.shadertoy.com/view/4t3BW4
        
    for(int k=0; k<4; k++){
        vec2 id = floor(uv*level);
        float frnd = hash22(id).x;
        
        for(int j=-1; j<=1; j++){
            for(int i=-1; i<=1; i++){
            
                // neighboring cell ID.
                vec2 neighbors = vec2(i,j);
                vec2 home = id+neighbors;
                vec2 rnd = hash22(home);
      
                // neighboring cell hash values.
                vec2 rnd2 = hash22(floor(home/2.));
                vec2 rnd4 = hash22(floor(home/4.));
                vec2 rnd8 = hash22(floor(home/8.));

                // If the previous large tile has been rendered, continue.
                if(k==1 && rnd2.y<rndX[0].y) continue;
                // If any of the two previous larger tiles have been rendered, continue.
                if(k==2 && (rnd2.y<rndX[1].y || rnd4.y<rndX[0].y)) continue;
                // If any of the three previous larger tiles have been rendered, continue.
                if(k==3 && (rnd2.y<rndX[2].y || rnd4.y<rndX[1].y  || rnd8.y<rndX[0].y )) continue;

                // threshold
                if(rnd.y<rndX[k].y) {
     
                    vec2 p = uv -(id+.5+neighbors)/level;
                    if(rnd.x<rndX[k].x) p.x = -p.x;
  
                    float bx = (.5)/level;
                    float tk = .16666/level;
                    float hf = .5/level;
                    float qf = .25/level;
                    float df = qf/3.;
                    float mf = .05/level;
                    
                    float square = box(p,vec2(bx));
                    vec2 pv = length(p-vec2(hf,-hf))<length(p+vec2(hf,-hf))? p-vec2(hf,-hf):p+vec2(hf,-hf);
   
                    // truchet
                    float cd = abs(length(pv)-hf)-tk;
                    
                    // all the dots
                    float bc = length(abs(p)-hf)-tk;
                    float bd = min(length(vec2(abs(p.x)-hf,p.y))-tk,length(vec2(p.x,abs(p.y)-hf))-tk);
                    float be = length(vec2(abs(p.x)-hf,abs(p.y)-qf))-df;
                    be = min(length(vec2(abs(p.x)-qf,abs(p.y)-hf))-df,be);
                    
                    // alt title
                    if(rnd.x>.8) cd = min(length(p.x),length(p.y))-tk;
                    // rnd blank
                    if(rnd.x<.1&&(k==0||k==1)) cd = 1e5;
                    
                    cd = max(cd,square);
                    cd = min(cd,bd);
         
                    if(mod(float(k),2.)==0.) {
                        cd = min(cd,bc);
                    } 
                    
                    grid = min(grid, abs(square)-.0025/2.);
                    s[k] = min(s[k], square);
                    d[k] = min(d[k], cd);
                    b[k] = min(b[k], bd);
                    e[k] = min(e[k], be);
                }
            }
        }    
        
        level*=2.;
    }
    
    float px = fwidth(uv.x);
    float ml = 1e5;

    float cvv = uv.x + .25*sin(uv.y*PI2+T*.4);
    cvv += uv.y + .25*sin(uv.x*PI+T*.3);
    vec3 cb = (suv.x>0.)? vec3(.8) : hsv(vec3(floor(cvv*6.)*2.045,1.,.5));
        
    vec3 C = vec3(.005);

    C = mix(C,vec3(.05),smoothstep(px,-px,grid));
    
    vec4 tk = vec4(1e5);
    for(int k=0; k<4; k++){

        
        if(mod(float(k),2.)==0.){
            d[k]=max(min(d[k],-s[k]),-e[k]);
            tk.x=min(tk.x,-d[k]);
            C = mix(C,cb,smoothstep(-px,px,d[k]));
        }else {
            tk.x=min(tk.x,d[k]);
            C = mix(C,cb,smoothstep(px,-px,d[k]));
        }

    }
    
    if(suv.x<0.) C = mix(C,vec3(1),smoothstep(px,-px,abs(tk.x)-.005));   
    if(suv.x<0.) C = mix(C,vec3(1),smoothstep(px,-px,abs(tk.x+.06)-.0075));
    //if(M.z>0.) C = mix(C,vec3(.7),smoothstep(px,-px,grid));
    if(suv.x>0.&&suv.x<.0025) C = vec3(1);
    
    // Output to screen
    C = pow(C,vec3(.4545));
    fragColor = vec4(C,1.);
}

</script>
<!-- partial -->
  <script src='https://jhancock532.github.io/twgl.min.js'></script><script  src="./script.js"></script>

</body>
</html>
