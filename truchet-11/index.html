<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Truchet 11</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/88/three.min.js"></script>
<script id="vertexShader" type="x-shader/x-vertex">
    void main() {
        gl_Position = vec4( position, 1.0 );
    }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  
  // #define GRID
  // #define ANIMATING
  
  const float lineWidth = .15;
  
  #define PI 3.141592653589793
  #define TAU 6.283185307179586
  
  const vec2 s = vec2(1, 1.7320508);

  vec2 hash2(vec2 p)
  {
    vec2 o = texture2D( u_noise, (p+0.5)/256.0, -100.0 ).xy;
    return o;
  }
  
	float ease(float t, float b, float c, float d) {
		if (t==0.) return b;
		if (t==d) return b+c;
		if ((t/=d/2.) < 1.) return c*.5 * pow(2., 10. * (t - 1.)) + b;
		return c*.5 * (-pow(2., -10. * --t) + 2.) + b;
	}
  
  
  // hex(uv)
  // getHex(uv)
  // -------------
  // These very very helpful functions are by Gary Warne.
  // -------------
  float hex(in vec2 p){

      p = abs(p);

      return max(dot(p, s*.5), p.x); // Hexagon.

  }
  
  vec4 getHex(vec2 p){

      vec4 hC = floor(vec4(p, p - vec2(.5, 1))/s.xyxy) + .5;

      // Centering the coordinates with the hexagon centers above.
      vec4 h = vec4(p - hC.xy*s, p - (hC.zw + .5)*s);

      return dot(h.xy, h.xy)<dot(h.zw, h.zw) ? vec4(h.xy, hC.xy) : vec4(h.zw, hC.zw + vec2(.5, 1));

  }
  
  const float ringsize = .285;
  const float distance = .57;
  const float angle_increment = TAU/3.;
  const float largeringsize = .798;
  const float largedistance = .92;
  
  float shade(float df, float width, float aa, float ringsize) {
    return smoothstep(ringsize-width, ringsize+aa-width, df) - smoothstep(ringsize-aa+width, ringsize+width, df); 
  }
  
  float shade(float df, float width, float aa) {
    return shade(df, width, aa, ringsize);
    // return smoothstep(ringsize-width, ringsize+aa-width, df) - smoothstep(ringsize-aa+width, ringsize+width, df);
  }
  
  float shade(float df, float width) {
    return shade(df, width, 0.005);
  }
  
  vec3 truchet(vec4 tile) {
    vec2 hash = hash2(tile.zw);
    float id = floor(hash.x*6.);
    vec4 _tile = mod(tile, 1.);
    float initial_rotation = 0.;
    
    float iso = hex(tile.xy);
    vec3 colour = vec3(1. - iso * 2.);
    colour = vec3(0.);
    
    float t = u_time * .2 + 1.;
    
    float animation_offset = hash.y * 2. + u_time;
    float animation_time = t + animation_offset;
    float prev_rotation = floor(animation_time / 6.);
    float next_rotation = floor(animation_time / 6.) - 1.;
    
    const float sides = 3.;
    
    float _shade;
    
    float easeVal = clamp(mod(animation_time, 6.) - 3., 0., 3.);
    easeVal = ease(easeVal, prev_rotation, prev_rotation-next_rotation, 3.);
    
    if(hash.y <= .3) {
      initial_rotation = TAU / 6. * 3.;
    } else if(hash.y <= .6) {
      initial_rotation = TAU / 6. * 2.;
    } else {
      initial_rotation = TAU / 6.;
    }
    #ifdef ANIMATING
      initial_rotation += TAU / 6. * easeVal;
    #endif
    
    // id = 1.;
    
    if(id <= 2.) {
      float _i = .25;
      float increment = initial_rotation + angle_increment * _i;
      float mask = shade(length(tile.xy + vec2(cos(increment), sin(increment)) * distance), lineWidth, 0.01, ringsize);
      colour = clamp(colour, 0., 1.);
      colour += mask;
      
      increment = initial_rotation + angle_increment * 1.5;
      mask = shade(length(tile.xy + vec2(cos(increment), sin(increment)) * largedistance), lineWidth, 0.01, largeringsize);
      colour = clamp(colour, 0., 1.);
      colour += mask;
      
      
      increment = initial_rotation + angle_increment * 2.;
      mask = shade(length(tile.xy + vec2(cos(increment), sin(increment)) * largedistance), lineWidth, 0.01, largeringsize);
      float _shade = shade(length(tile.xy + vec2(cos(increment), sin(increment)) * largedistance), lineWidth+.1, 0.14, largeringsize);
      colour -= _shade;
      colour = clamp(colour, 0., 1.);
      colour += mask;
    } else {
      for(float i = 1.; i <= sides; i++) {
        float _i = i - .25;
        float increment = initial_rotation + angle_increment * _i;
        float mask = shade(length(tile.xy + vec2(cos(increment), sin(increment)) * distance), lineWidth, 0.01, ringsize);
        colour = clamp(colour, 0., 1.);
        colour += mask;
      }
    }
    
    
    
    return colour;
    
  }

  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);
    
    float y = sin(u_time*.2) * 5.;
    float x = u_time*2.;
    float c = cos(u_time * .3);
    float s = sin(u_time * .3);
    
    uv *= mat2(c, -s, s, c);
    uv *= 8. + sin(u_time * .5) * 3. ;
    
    uv.y += y;
    uv.x += x;
    
    
    
    vec4 hex_uv = getHex(uv);
    
    vec3 colour = truchet(hex_uv);
    
    #ifdef GRID
      float iso = hex(hex_uv.xy);
      colour = mix(colour, vec3(1., 1., 0.), smoothstep(.49, .5, iso));
    #endif

    gl_FragColor = vec4(colour,1.0);
  }
</script>


<div id="container" touch-action="none"></div>
<!-- partial -->
  <script  src="./script.js"></script>

</body>
</html>
